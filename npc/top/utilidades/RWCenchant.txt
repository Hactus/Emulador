prontera,148,165,3	script	Driller#pron	4W_M_02,{
	disable_items;
	if (checkweight(Knife,1) == 1000) {
		mes "Você está carregando muitos itens em seu inventário. Visite a Kafra e tente novamente.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "Você não pode prosseguir porque está com excesso de peso.";
		close;
	}
	.@part = EQI_ACC_L;
	if (!getequipisequiped(.@part)) {
		mes "[Driller]";
		mes "Meu trabalho é adicionar slots em acessórios RWC .";
		next;
		mes "[Driller]";
		mes "Me desculpe, mas você não tem nenhum item RWC equipado no lado direito da aba de acessórios.";
		close;
	}
	mes "[Driller]";
	mes "Meu trabalho é adicionar slots em acessórios RWC. Então eu apenas irei trabalhar com ^ff0000Itens Puros^000000, ou aqueles que não foram encantados.";
	next;
	.@equip_id = getequipid(.@part);
	if (.@equip_id != 2966 && .@equip_id != 2968) {
		mes "[Driller]";
		mes "No entanto, posso ver que o acessório que você está usando não pode ser tratado. Por favor equipe um RWC Memorial accessory.";
		close;
	}
	mes "[Driller]";
	mes "Você também deve saber que adicionar um slot é extremamente perigoso. ^ff0000As chances de sucesso são cerca de 50%.^000000 Você deseja continuar?";
	next;
	if(select("Cancelar", "Vamos lá!") == 1) {
		mes "[Driller]";
		mes "Até mais.";
		close;
	}
	if (.@equip_id == 2966) {
		.@slotted = 2967; //RWC_2012_Ring_
		.@name$ = "RWC 2012 Memorial Ring";
		.@str$ = "ring";
	} else if (.@equip_id == 2968) {
		.@slotted = 2969; //RWC_2012_Pendant_
		.@name$ = "RWC 2012 Memorial Pendant";
		.@str$ = "pendant";
	} else {
		mes "[Driller]";
		mes "Não consigo identificar o acessório que você possui. Eu não posso trabalhar nisso.";
		close;
	}
	if (getequipcardid(.@part,3) > 0) {
		mes "[Driller]";
		mes "Este item já foi encantado. Não consigo trabalhar com isso, pois é contra as regras.";
		close;
	}
	delequip .@part;
	if (rand(1,10) > 5) {
		getitem .@slotted,1;
		specialeffect(EF_REPAIRWEAPON, AREA, playerattached());
		mes "[Driller]";
		mes "Issoooo!!!! Successo! "+.@name$+", agora você possui um item slotado. Confira!";
		close;
	} else {
		specialeffect(EF_LORD, AREA, playerattached());
		mes "[Driller]";
		mes "Awww... Maldito "+.@str$+"... Ele quebrou durante o procedimento. Eu sinto Muito.";
		close;
	}
}

prontera,148,163,3	script	Goldberg#pron	4_M_OILMAN,{
	disable_items;
	if (checkweight(Knife,1) == 1000) {
		mes "Você está carregando muitos itens. Volte depois de ter organizado o seu inventário.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "Você não pode prosseguir porque está com excesso de peso.";
		close;
	}
	mes "[Goldberg]";
	mes "Olá! Estou a cargo dos encantadores acessórios RWC Memorial com alguns poderes místicos.";
	next;
	.@part = EQI_ACC_L;
	if (!getequipisequiped(.@part)) {
		mes "[Goldberg]";
		mes "Me desculpe, mas você não tem nenhum acessório equipado.";
		close;
	}
	.@equip_id = getequipid(.@part);
	if (.@equip_id < 2966 || .@equip_id > 2969) {
		mes "[Goldberg]";
		mes "No entanto, posso ver que o acessório que você está usando não pode ser tratado. Por favor equipe um RWC Memorial accessory.";
		close;
	}
	.@select = select("Desculpe, não estou interessado.", "Por favor, encante meu acessório.", "Remover Encantamento.")-1;
	if (.@select == 0) {
		mes "[Goldberg]";
		mes "Tudo bem, então, vejo você na próxima vez...";
		close;
	}
	.@equip_refine = getequiprefinerycnt(.@part);
	setarray .@equip_card[0], getequipcardid(.@part,0),getequipcardid(.@part,1),getequipcardid(.@part,2),getequipcardid(.@part,3);
	if (.@select == 1) {
		switch(getequipid(.@part)) {
		case 2966:
			setarray .@option[0],2,2,4,4;
			break;
		case 2967:
			setarray .@option[0],0,2,4,4;
			break;
		case 2968:
			setarray .@option[0],1,1,3,3;
			break;
		case 2969:
			setarray .@option[0],0,1,3,3;
			break;
		default:
			mes "[Goldberg]";
			mes "Me desculpe, mas não consigo trabalhar com o acessório que você está equipado.";
			mes "Se você equipou seu acessório RWC Memorial no lado esquerdo, tente trocá-lo para o lado direito.";
			close;
		}
		for(.@i = 3; .@i >= 0; --.@i) {
			if (.@equip_card[.@i] == 0) {
				.@slot = .@i;
				.@op_type = .@option[.@i];
				break;
			}
		}
		switch(.@op_type) {
		case 4:
			mes "[Goldberg]";
			mes "Qual encantamento você gostaria de inserir?";
			next;
			setarray .@enchant_select[0],1,2,3,4;
			.@i = select("Nenhum", "Espírito de lutador", "ATQ (%)", "HP Max", "HP")-2;
			break;
		case 3:
			setarray .@enchant_select[0],5,6,7;
			.@i = select("Cancelar", "Conjuração", "ATQM (%)", "SP")-2;
			break;
		case 2:
			mes "[Goldberg]";
			mes "^ff0000Seja cuidadoso! Há cerca de 25% de chance do encantamento falhar. Se isso acontecer, o item será destruído.^000000 Qual encantamento você gostaria de inserir?";
			next;
			setarray .@enchant_select[0],8,9,10,11,12,13,14;
			.@i = select("Cancel", "STR", "AGI", "VIT", "INT", "DEX", "LUK", "SP")-2;
			break;
		case 1:
			mes "[Goldberg]";
			mes "^ff0000Seja cuidadoso! Há cerca de 25% de chance do encantamento falhar. Se isso acontecer, o item será destruído.^000000 Qual encantamento você gostaria de inserir?";
			next;
			setarray .@enchant_select[0],8,9,10,11,12,13,15,16;
			.@i = select("Cancelar", "FOR", "AGI", "VIT", "INT", "DES", "SOR", "HPMax", "HP")-2;
			break;
		case 0:
			mes "[Goldberg]";
			mes "Seu acessório já recebeu tantos encantamentos que eu não posso mais trabalhar com isso.";
			close;
		}
		if (.@i == -1) {
			mes "[Goldberg]";
			mes "Tudo bem, então, vejo você na próxima vez.";
			close;
		}
		mes "[Goldberg]";
		mes "O poder do encantamento será escolhido aleatoriamente. ^ff0000Uma vez inserido, o encantamento não pode ser removido.^000000 Deseja continuar?";
		next;
		if(select("Não, por favor pare.", "Sim, Proceda.") == 1) {
			mes "[Goldberg]";
			mes "Tudo bem, então, vejo você na próxima vez...";
			close;
		}
		.@enchant_type = .@enchant_select[.@i];
		if (!getequipisequiped(.@part)) {
			mes "[Goldberg]";
			mes "Não tire seu equipamento enquanto estou trabalhando, ok?";
			close;
		}
		switch(.@enchant_type) {
		case 1:
			setarray .@enc[0],4811,4810,4809; //Fighting_Spirit1,Fighting_Spirit2,Fighting_Spirit3
			break;
		case 2:
			setarray .@enc[0],4819,4766,4767; //Atk1,Atk2,Atk3
			break;
		case 3:
			setarray .@enc[0],4861,4862,4867; //MHP1,MHP2,MHP3
			break;
		case 4:
			setarray .@enc[0],4795,4796,4797; //HP100,HP200,HP300
			break;
		case 5:
			setarray .@enc[0],4760,4761,4806; //Matk1,Matk2,Matk3
			break;
		case 6:
			setarray .@enc[0],4815,4814,4813; //Spell1,Spell2,Spell3
			break;
		case 7:
			setarray .@enc[0],4870,4800,4871; //SP25,SP50,SP75
			break;
		case 8:
			setarray .@enc[0],4700,4701,4702; //Strength1,Strength2,Strength3
			break;
		case 9:
			setarray .@enc[0],4730,4731,4732; //Agility1,Agility2,Agility3
			break;
		case 10:
			setarray .@enc[0],4740,4741,4742; //Vitality1,Vitality2,Vitality3
			break;
		case 11:
			setarray .@enc[0],4710,4711,4712; //Inteligence1,Inteligence2,Inteligence3
			break;
		case 12:
			setarray .@enc[0],4720,4721,4722; //Dexterity1,Dexterity2,Dexterity3
			break;
		case 13:
			setarray .@enc[0],4750,4751,4752; //Luck1,Luck2,Luck3
			break;
		case 14:
			setarray .@enc[0],4870,4800,4871; //SP25,SP50,SP75
			break;
		case 15:
			setarray .@enc[0],4861,4862,4867; //MHP1,MHP2,MHP3
			break;
		case 16:
			setarray .@enc[0],4795,4796,4797; //HP100,HP200,HP300
			break;
		default:
			mes "[Goldberg]";
			mes "Temos um problema, deixe-me verificá-lo.";
			close;
		}

		if (.@enchant_type < 8)
			.@i = rand(1,300); // 0% break chance.
		else
			.@i = rand(1,400); // 25% break chance.

		     if (.@i < 151) .@enchant = .@enc[0];
		else if (.@i < 251) .@enchant = .@enc[1];
		else if (.@i < 301) .@enchant = .@enc[2];
		else .@enchant = 9;

		.@equip_card[.@slot] = .@enchant;
		if (.@slot == 2 && .@enchant == 0) {
			.@equip_card[3] = 0;
		} else if (.@slot == 1 && .@enchant == 0) {
			.@equip_card[2] = 0;
			.@equip_card[3] = 0;
		} else if (.@slot == 0 && .@enchant == 0) {
			.@equip_card[1] = 0;
			.@equip_card[2] = 0;
			.@equip_card[3] = 0;
		}

		delequip .@part;
		if (.@enchant == 9) {
			mes "[Goldberg]";
			mes "Oh, Deus!";
 			mes "O item não era forte o suficiente para suportar o encantamento e, assim, acabou sendo destruído. Sinto muito.";
			specialeffect(EF_LORD, AREA, playerattached());
			close;
		}
		if (.@enchant == 0) { // Should never happen.
			mes "[Goldberg]";
			mes "Oh ... Parece que houve uma instabilidade de algum tipo entre todos os poderes inseridos. Isso fez com que todos os encantamentos desaparecessem. É uma pena, mas tente novamente!";
		} else {
			mes "[Goldberg]";
			mes "Ótimo!";
			mes "O encantamento foi um sucesso! Será aplicado no item ^990000"+(.@slot+1)+"^000000.";
			specialeffect(EF_REPAIRWEAPON, AREA, playerattached());
		}

		//GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	} else if (.@select == 2) {
		mes "[Goldberg]";
		mes "Eu apenas inicializarei a opção de encantar sem fazer nada no slot de cartas. Você quer continuar?";
		next;
		if(select("Não, obrigado.", "Sim, claro continue.") == 1) {
			mes "[Goldberg]";
			mes "Volte se você mudar de ideia.";
			close;
		}
		if (countitem(6665) == 0) {
			mes "[Goldberg]";
			mes "Eu sinto Muito. Mas você não possui o cupom RWC. Você pode verificar o seu inventário?";
			close;
		}
		if (.@equip_card[3] == 0) {
			mes "[Goldberg]";
			mes "Hm ... este equipamento está limpo. Eu não posso inicializá-lo se não houver nada! Verifique novamente.";
			close;
		}
		specialeffect(EF_REPAIRWEAPON, AREA, playerattached());
		mes "[Goldberg]";
		mes "A opção de encantar no seu item será inicializada.";
		delitem 6665,1;
		delequip .@part;

		//GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		for(.@i = 0; .@i < 4; ++.@i) {
			if (.@equip_card[.@i] >= 4700) // Esquema de encantar armadura
				.@equip_card[.@i] = 0;
		}
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	}
}