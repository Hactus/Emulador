/*.---------------------------------------------------------------------.
  .		 ____                          __                          		.
  .		/\  _`\                       /\ \__  __                   		.
  .		\ \ \/\_\  _ __    __     __  \ \ ,_\/\_\  __  __     __   		.
  .		 \ \ \/_/_/\`'__\/'__`\ /'__`\ \ \ \/\/\ \/\ \/\ \  /'__`\ 		.
  .		  \ \ \s\ \ \ \//\  __//\ \d\.\_\ \ \_\ \ \ \ \_/ |/\  __/ 		.
  .		   \ \____/\ \_\\ \____\ \__/.\_\\ \__\\ \_\ \___/ \ \____\		.
  .		    \/___/  \/_/ \/____/\/__/\/_/ \/__/ \/_/\/__/   \/____/		.
  .																		.
  .				2014~2015 © Creative Services and Developent			.
  .						  www.creativesd.com.br							.
  .					    www.sbkmod.ragnagreen.com                       .
  .---------------------------------------------------------------------.
  . Script:													            .
  .	   1º Desfiladeiro de Tierra                                        .
  .---------------------------------------------------------------------.
  . Autor: Romulo SM (sbk_)                                 Versão: 4.7	.
  .                                                                     .
  . Compátivel:	rAthena / Hercules e derivações.                    	.
  .---------------------------------------------------------------------.
  . Descrição:															.
  .	   Batalha no qual os jogadores devem destruir o suprimento do      .
  .	   Exército adversário.                                             .
  *---------------------------------------------------------------------*/
-	script	BG_Tierra_01	-1,{
OnReadyCheck:
	if( $@TierraBG1 )
		end;
		
	set $@TierraBG1,1;
	set $@TierraBG1_CroixScore, 0;
	set $@TierraBG1_GuillScore, 0;
	bg_updatescore "bat_a01",$@TierraBG1_GuillScore,$@TierraBG1_CroixScore;
	donpcevent "start#bat_a01::OnEnable";
	sleep 10000;
	bg_warp $@BG_1_id1,"bat_a01",352,342;
	bg_warp $@BG_1_id2,"bat_a01",353,52;
	initnpctimer "#bat_a01_timer";
	sleep 5000;
	set .@guill_colonel, callfunc("bg_queue_func", 11, 1, 0);
	set .@croix_colonel, callfunc("bg_queue_func", 11, 1, 1);
	sleep 5000;
	mapannounce "bat_a01", "Tenente de Guillaume Axl Rose: O Exército de Guillaume está sobre o comando do Comandante " + callfunc("bg_getcharname", .@guill_colonel) + ".",bc_map,"0xFFCE00";
	sleep 3000;
	mapannounce "bat_a01", "Tenente de Croix Swandery: O Exército de Croix está sobre o comando do Comandante " + callfunc("bg_getcharname", .@croix_colonel) + ".",bc_map,"0xFFCE00";
	end;

OnGuillaumeDie:
OnCroixDie:
	end;
	
OnNewJoin:
	set .@bg_team, getcharid(4);
	set .@bg_x, bg_team_getxy(.@bg_team, 0);
	set .@bg_y, bg_team_getxy(.@bg_team, 1);
	set .@char_id, getcharid(4);
	get_bg_team .@bg_team;
	copyarray .@bg_members[0], $@bg_members, getarraysize($@bg_members);
	for( set .@i, 0; .@i < getarraysize(.@bg_members); set .@i, .@i + 1 )
	{
		set .@name$, callfunc("bg_getcharname", .@bg_members[.@i]);
		if( .@name$ != "" && .@name$ != strcharinfo(0) )
			message .@name$, "O Jogador [" + strcharinfo(0) + "] ingressou no exército de " + (.@bg_team==$@BG_1_id1?"Guillaume":"Croix")+".";
	}
	
	set .@position, (!getarraysize(.@bg_members)?5:getarraysize(.@bg_members));
	set .@army, (getcharid(4)==$@BG_1_id1?0:1);
	update_bg_position(.@position,.@army,strcharinfo(0));
	message strcharinfo(0), "Você será teleportado em 10 segundos.";
	sleep2 10000;
	warp "bat_a01", .@bg_x, .@bg_y;
	end;

OnGuillaumeQuit:
	bg_leave;
	set .@char_id, getcharid(0);
	callsub sCheckWO, .@char_id, 0;
	callfunc "bg_queue_func", 4, 1, .@char_id, 0, 1; // BG Queue Ranking: Computer negative vote.
	end;
	
OnCroixQuit:
	bg_leave;
	set .@char_id, getcharid(0);
	callsub sCheckWO, .@char_id, 1;
	callfunc "bg_queue_func", 4, 1, .@char_id, 0, 1; // BG Queue Ranking: Computer negative vote.
	end;
	
OnCheckWO:
	set .@char_id, getcharid(0);
	set .@army, (getcharid(4)==$@BG_1_id1?0:1);
	bg_leave;
	callsub sCheckWO, .@char_id, .@army;
	callfunc "bg_queue_func", 4, 1, .@char_id, 0, 1; // BG Queue Ranking: Computer negative vote.
	end;
	
sCheckWO:
	// Remove o Jogador do Exército na Lista de Jogando.
	callfunc "bg_queue_func", 10, getarg(0);
	
	// Exército de Guillaume.
	set .@guillaume, callfunc("bg_queue_func", 5, 1, 0);
	// Exército de Croix.
	set .@croix, callfunc("bg_queue_func", 5, 1, 1);
	
	if( $@TierraBG1 < 2 && (.@guillaume == 0 || .@croix == 0) )
	{
		killmonsterall "bat_a01";
		donpcevent "Terapeuta#a01_a::OnStop";
		donpcevent "Terapeuta#a01_b::OnStop";
		set $@TierraBG1_Victory, (.@guillaume==0?2:1);
		enablenpc "Tenente de Guillaume#a01_a";
		enablenpc "Tenente de Croix#a01_b";
		bg_warp $@BG_1_id1,"bat_a01",50,374;
		bg_warp $@BG_1_id2,"bat_a01",42,16;
		set $@TierraBG1, 2;
		sleep 2000;
		if( .@guillaume == 0 )
			mapannounce "bat_a01", "Tenente de Croix Swandery: Os covardes de Guillaume fugiram. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
		else
			mapannounce "bat_a01", "Tenente de Guillaume Axl Rose: Os covardes de Croix fugiram. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
	}
	else {
		sleep 5000;
		callfunc "bg_queue_func", 12, 1, getarg(1);
	}
	return;
}

-	script	#bat_a01_timer	-1,{
	end;
	
OnTimer60000:
	stopnpctimer;
	
	if( !getmapusers("bat_a01") )
	{
		set $@TierraBG1, 0;
		callfunc "bg_queue_func", 8, 1;
	}
	else
		initnpctimer;
		
	end;
}

// Tierra Gorge Battleground Engine
//============================================================
bat_a01,15,15,3	script	start#bat_a01	CLEAR_NPC,{
	end;

OnEnable:
	donpcevent "OBJ#bat_a01_a::OnKill";
	donpcevent "OBJ#bat_a01_a::OnEnable";
	donpcevent "OBJ#bat_a01_b::OnKill";
	donpcevent "OBJ#bat_a01_b::OnEnable";
	donpcevent "barricade#bat_a01_a::OnKill";
	donpcevent "barricade#bat_a01_a::OnEnable";
	donpcevent "barricade#bat_a01_b::OnKill";
	donpcevent "barricade#bat_a01_b::OnEnable";
	donpcevent "OBJ#bat_a01_n::OnKill";
	donpcevent "OBJ#bat_a01_n::OnEnable";
	donpcevent "NOBJ_mob#bat_a01_a::OnKill";
	donpcevent "NOBJ_mob#bat_a01_b::OnKill";
	donpcevent "Terapeuta#a01_a::OnEnable";
	donpcevent "Terapeuta#a01_b::OnEnable";
	donpcevent "countdown#bat_a01::OnEnable";
	disablenpc "Ferreiro Guillaume#a01";
	disablenpc "Ferreiro Croix#bat_a01";
	disablenpc "Tenente de Guillaume#a01_a";
	disablenpc "Tenente de Croix#a01_b";
	end;
}

bat_a01,15,16,3	script	OBJ#bat_a01_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@BG_1_id1,"bat_a01",176,345,"Suprimentos",1910,"OBJ#bat_a01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","OBJ#bat_a01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","OBJ#bat_a01_a::OnMyMobDead") < 1) {
		set $@TierraBG1_CroixScore, $@TierraBG1_CroixScore + 1;
		bg_updatescore "bat_a01", $@TierraBG1_GuillScore, $@TierraBG1_CroixScore;
		if( $@TierraBG1_CroixScore >= atoi(callfunc("bg_getarenainfo",1,"score_round")) )
		{
			donpcevent "Terapeuta#a01_a::OnStop";
			donpcevent "Terapeuta#a01_b::OnStop";
			set $@TierraBG1_Victory, 2;
			enablenpc "Tenente de Guillaume#a01_a";
			enablenpc "Tenente de Croix#a01_b";
			mapannounce "bat_a01", "Tenente de Croix Swandery: Nós destruímos os Suprimentos dos Guillaumes. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
			bg_warp $@BG_1_id1,"bat_a01",50,374;
			bg_warp $@BG_1_id2,"bat_a01",42,16;
			set $@TierraBG1, 2;
		}
		else {
			// Evento de rounds.
			donpcevent "start#bat_a01::OnEnable";
			bg_warp $@BG_1_id1,"bat_a01",352,342;
			bg_warp $@BG_1_id2,"bat_a01",353,52;
		}
	}
	end;
}

bat_a01,15,17,3	script	OBJ#bat_a01_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@BG_1_id2,"bat_a01",167,50,"Suprimentos",1910,"OBJ#bat_a01_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","OBJ#bat_a01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","OBJ#bat_a01_b::OnMyMobDead") < 1) {
		set $@TierraBG1_GuillScore, $@TierraBG1_GuillScore + 1;
		bg_updatescore "bat_a01", $@TierraBG1_GuillScore, $@TierraBG1_CroixScore;
		if( $@TierraBG1_GuillScore >= atoi(callfunc("bg_getarenainfo",1,"score_round")) )
		{
			donpcevent "Terapeuta#a01_a::OnStop";
			donpcevent "Terapeuta#a01_b::OnStop";
			set $@TierraBG1_Victory, 1;
			enablenpc "Tenente de Guillaume#a01_a";
			enablenpc "Tenente de Croix#a01_b";
			mapannounce "bat_a01", "Tenente de Guillaume Axl Rose : Nós destruímos os Suprimentos dos Croixs. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
			bg_warp $@BG_1_id1,"bat_a01",50,374;
			bg_warp $@BG_1_id2,"bat_a01",42,16;
			set $@TierraBG1, 2;
		}
		else {
			// Evento de rounds.
			donpcevent "start#bat_a01::OnEnable";
			bg_warp $@BG_1_id1,"bat_a01",352,342;
			bg_warp $@BG_1_id2,"bat_a01",353,52;
		}
	}
	end;
}

bat_a01,15,18,3	script	barricade#bat_a01_a	CLEAR_NPC,{
OnEnable:
	for( set .@i,185; .@i < 201; set .@i,.@i+1 )
		bg_monster $@BG_1_id1,"bat_a01",.@i,266,"Barricada",1906,"barricade#bat_a01_a::OnMyMobDead";
	setwall "bat_a01",186,266,16,6,1,"bat_a01_c1";
	end;

OnKill:
	killmonster "bat_a01","barricade#bat_a01_a::OnMyMobDead";
	delwall "bat_a01_c1";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","barricade#bat_a01_a::OnMyMobDead") < 17) {
		killmonster "bat_a01","barricade#bat_a01_a::OnMyMobDead";
		delwall "bat_a01_c1";
		enablenpc "Ferreiro Guillaume#a01";
		mapannounce "bat_a01", "Tenente de Guillaume Axl Rose : A Barricada do vale foi destruída! Onde está o nosso Ferreiro Guillaume? Nós precisamos reconstruir a Barricada!",bc_map,"0xFFCE00";
	}
	end;
}

bat_a01,15,19,3	script	barricade#bat_a01_b	CLEAR_NPC,{
OnEnable:
	for( set .@i,169; .@i < 186; set .@i,.@i+1 )
		bg_monster $@BG_1_id2,"bat_a01",.@i,129,"Barricada",1906,"barricade#bat_a01_b::OnMyMobDead";
	setwall "bat_a01",170,129,16,6,1,"bat_a01_g1";
	end;

OnKill:
	killmonster "bat_a01","barricade#bat_a01_b::OnMyMobDead";
	delwall "bat_a01_g1";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","barricade#bat_a01_b::OnMyMobDead") < 17) {
		killmonster "bat_a01","barricade#bat_a01_b::OnMyMobDead";
		delwall "bat_a01_g1";
		enablenpc "Ferreiro Croix#bat_a01";
		mapannounce "bat_a01", "Tenente de Croix Swandery : A Barricada do vale foi destruída! Onde está o nosso Ferreiro Croix? Nós precisamos reconstruir a Barricada!",bc_map,"0xFFCE00";
	}
	end;
}

bat_a01,15,20,3	script	OBJ#bat_a01_n	CLEAR_NPC,{
OnEnable:
	monster "bat_a01",273,203,"Bandeira Neutra",1911,1,"OBJ#bat_a01_n::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","OBJ#bat_a01_n::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","OBJ#bat_a01_n::OnMyMobDead") < 17) {
		bg_team_setxy getcharid(4),56,212;
		if (getcharid(4) == $@BG_1_id1) {
			donpcevent "NOBJ_mob#bat_a01_a::OnEnable";
			mapannounce "bat_a01", "O time Guillaume capturou a Bandeira Neutra, então eles estão em vantagem..",bc_map,"0xFFCE00";
		}
		else {
			donpcevent "NOBJ_mob#bat_a01_b::OnEnable";
			mapannounce "bat_a01", "O time Croix capturou a Bandeira Neutra, então eles estão em vantagem.",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a01,15,21,3	script	NOBJ_mob#bat_a01_a	CLEAR_NPC,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a01_b::OnKill";
	bg_monster $@BG_1_id1,"bat_a01",272,204,"Guardião de Guillaume",1949,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@BG_1_id1,"bat_a01",272,213,"Guardião de Guillaume",1949,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@BG_1_id1,"bat_a01",273,197,"Guardião de Guillaume",1950,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","NOBJ_mob#bat_a01_a::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a01,15,22,3	script	NOBJ_mob#bat_a01_b	CLEAR_NPC,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a01_a::OnKill";
	bg_monster $@BG_1_id2,"bat_a01",272,204,"Guardião de Croix",1949,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@BG_1_id2,"bat_a01",272,213,"Guardião de Croix",1949,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@BG_1_id2,"bat_a01",273,197,"Guardião de Croix",1950,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","NOBJ_mob#bat_a01_b::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a01,185,270,1	script	Ferreiro Guillaume#a01	4_M_REPAIR,{
	if (getcharid(4) == $@BG_1_id1) {
		mes "[Ferreiro Guillaume]";
		mes "Nós estamos em uma emergência! A Barricada foi destruída!";
		mes "Nós podemos reparar a Barricada com ^3131FF50 Pedras, 3 Nervos de Urso, 500 Pedaços de Metal, 30 Minérios de Elunium e 100 Ouro.^000000";
		mes "E nós temos tudo isso, exceto as 50 Pedras!";
		next;
		switch(select("Reparar.:Deixar assim.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Ferreiro Guillaume]";
				mes "Você trouxe pedras o suficiente! Vamos então começar a reparar.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal, e trançando as pedras com os Nervos de Urso!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Ferreiro Guillaume]";
				mes "Uau! Está feito.";
				mes "Estamos mais aliviados agora.";
				delitem 7049,50; //Stone
				donpcevent "barricade#bat_a01_a::OnEnable";
				close2;
				disablenpc "Ferreiro Guillaume#a01";
				end;
			}
			else {
				mes "[Ferreiro Guillaume]";
				mes "Você não possui pedras o suficiente!";
				next;
				mes "[Ferreiro Guillaume";
				mes "^3131FFNós precisamos de 50 Pedras.^000000";
				mes "Estamos ocupados, então vá mais depressa.";
				close;
			}
		case 2:
			mes "[Ferreiro Guillaume]";
			mes "Os inimigos estão chegando! Vamos evacuar e partir daqui!";
			close;
		}
	}
	else {
		mes "[Ferreiro Guillaume]";
		mes "Lá, o inimigo está chegando";
		close;
	}

OnInit:
	disablenpc "Ferreiro Guillaume#a01";
	end;
}

bat_a01,170,121,5	script	Ferreiro Croix#bat_a01	4_M_REPAIR,{
	if (getcharid(4) == $@BG_1_id2) {
		mes "[Ferreiro Croix]";
		mes "Nós estamos em uma emergência! A Barricada foi destruída!";
		mes "Nós podemos reparar a Barricada com ^3131FF50 Pedras, 3 Nervos de Urso, 500 Pedaços de Metal, 30 Minérios de Elunium e 100 Ouro.^000000";
		mes "E nós temos tudo isso, exceto as 50 Pedras!";
		next;
		switch(select("Reparar.:Deixar assim.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Ferreiro Croix]";
				mes "Você trouxe pedras o suficiente! Vamos então começar a reparar.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Ferreiro Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada.";
				next;
				mes "[Ferreiro Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada.";
				next;
				mes "[Ferreiro Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte.";
				next;
				mes "[Ferreiro Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal.";
				next;
				mes "[Ferreiro Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal, e trançando as pedras com os Nervos de Urso!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Ferreiro Croix]";
				mes "Uau! Está feito.";
				mes "Estamos mais aliviados agora.";
				delitem 7049,50; //Stone
				donpcevent "barricade#bat_a01_b::OnEnable";
				close2;
				disablenpc "Ferreiro Croix#bat_a01";
				end;
			}
			else {
				mes "[Ferreiro Croix]";
				mes "Você não possui pedras o suficiente!";
				next;
				mes "[Ferreiro Croix]";
				mes "^3131FFNós precisamos de 50 Pedras.^000000";
				mes "Estamos ocupados, então vá mais depressa.";
				close;
			}
		case 2:
			mes "[Ferreiro Croix]";
			mes "Os inimigos estão chegando! Vamos evacuar e partir daqui!";
			close;
		}
	}
	else {
		mes "[Ferreiro Croix]";
		mes "Lá, o inimigo está chegando";
		close;
	}

OnInit:
	disablenpc "Ferreiro Croix#bat_a01";
	end;
}

bat_a01,53,377,3	script	Terapeuta#a01_a	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta de Batalha]";
	mes "Agora feche o seus olhos";
	mes "e respire bem fundo.";
	mes "Você pode se libertar da dor agora.";
	close;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Terapeuta#a01_a";
	end;

OnStop:
	disablenpc "bat_a01_rp1_a_warp";
	disablenpc "Terapeuta#a01_a";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a01_rp1_a_warp";
	end;

OnTimer26000:
	disablenpc "bat_a01_rp1_a_warp";
	end;

OnTimer26500:
	donpcevent "Terapeuta#a01_a::OnEnable";
	end;
}

bat_a01,51,375,0	script	bat_a01_rp1_a_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a01_rp1_a_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a01",352,342;
	end;
}

bat_a01,45,19,3	script	Terapeuta#a01_b	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta de Batalha]";
	mes "Agora feche o seus olhos";
	mes "e respire bem fundo.";
	mes "Você pode se libertar da dor agora.";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Terapeuta#a01_b";
	end;

OnStop:
	disablenpc "bat_a01_rp1_b_warp";
	disablenpc "Terapeuta#a01_b";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a01_rp1_b_warp";
	end;

OnTimer26000:
	disablenpc "bat_a01_rp1_b_warp";
	end;

OnTimer26500:
	donpcevent "Terapeuta#a01_b::OnEnable";
	end;
}

bat_a01,43,17,0	script	bat_a01_rp1_b_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a01_rp1_b_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a01",353,52;
	end;
}

bat_a01,60,216,3	script	Fantasma#bat_a01_n	4_GHOSTRING,{
	specialeffect2 EF_HEAL;
	mes "[Fantasma do Vale]";
	mes "Boo...Boo...";
	close;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a01_rp1_n_warp";
	end;

OnTimer26000:
	disablenpc "bat_a01_rp1_n_warp";
	end;

OnTimer26500:
	donpcevent "Fantasma#bat_a01_n::OnEnable";
	end;
}

bat_a01,55,213,0	script	bat_a01_rp1_n_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a01_rp1_n_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a01",301,209;
	end;
}

bat_a01,194,267,0	script	barri_warp_up#bat_a01_a	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@BG_1_id1)
		warp "bat_a01",194,261;
	end;
}

bat_a01,194,265,0	script	barri_warp_down#bat_a01a	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@BG_1_id1) 
		warp "bat_a01",194,270;
	end;
}

bat_a01,177,130,0	script	barri_warp_up#bat_a01_b	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@BG_1_id2)
		warp "bat_a01",178,125;
	end;
}

bat_a01,177,128,0	script	barri_warp_down#bat_a01b	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@BG_1_id2)
		warp "bat_a01",178,134;
	end;
}

bat_a01,169,227,0	script	underladd#bat_a01_1	WARPNPC,1,1,{
OnTouch:
	warp "bat_a01",178,228;
	end;
}

bat_a01,208,164,0	script	underladd#bat_a01_2	WARPNPC,1,1,{
OnTouch:
	warp "bat_a01",200,171;
	end;
}

bat_a01,171,309,3	script	Acampamento Guillaume#flag1	1_FLAG_LION,{ end; }
bat_a01,149,310,3	script	Acampamento Guillaume#flag2	1_FLAG_LION,{ end; }
bat_a01,119,336,3	script	Acampamento Guillaume#flag3	1_FLAG_LION,{ end; }
bat_a01,118,357,3	script	Acampamento Guillaume#flag4	1_FLAG_LION,{ end; }
bat_a01,150,380,3	script	Acampamento Guillaume#flag5	1_FLAG_LION,{ end; }
bat_a01,173,380,3	script	Acampamento Guillaume#flag6	1_FLAG_LION,{ end; }
bat_a01,210,344,3	script	Acampamento Guillaume#flag7	1_FLAG_LION,{ end; }
bat_a01,350,325,3	script	Acampamento Guillaume#flag8	1_FLAG_LION,{ end; }
bat_a01,358,325,3	script	Acampamento Guillaume#flag9	1_FLAG_LION,{ end; }

bat_a01,138,12,3	script	Acampamento Croix#flag1	1_FLAG_EAGLE,{ end; }
bat_a01,108,36,3	script	Acampamento Croix#flag2	1_FLAG_EAGLE,{ end; }
bat_a01,108,63,3	script	Acampamento Croix#flag3	1_FLAG_EAGLE,{ end; }
bat_a01,136,87,3	script	Acampamento Croix#flag4	1_FLAG_EAGLE,{ end; }
bat_a01,167,86,3	script	Acampamento Croix#flag5	1_FLAG_EAGLE,{ end; }
bat_a01,199,49,3	script	Acampamento Croix#flag6	1_FLAG_EAGLE,{ end; }
bat_a01,168,16,3	script	Acampamento Croix#flag7	1_FLAG_EAGLE,{ end; }
bat_a01,357,74,3	script	Acampamento Croix#flag8	1_FLAG_EAGLE,{ end; }
bat_a01,348,74,3	script	Acampamento Croix#flag9	1_FLAG_EAGLE,{ end; }

bat_a01,1,5,3	script	countdown#bat_a01	CLEAR_NPC,{
OnEnable:
	set $@tierra01_timer, 0;
	initnpctimer;
	sleep 7000;
	mapannounce "bat_a01", "Tenente de Guillaume Axl Rose: Vamos atacar e incinerar todo os Suprimentos do Croix!",bc_map,"0xFF9900";
	sleep 2000;
	mapannounce "bat_a01", "Tenente de Croix Swandery : Mestre do Valhalla! Presenteie-nos com coragem e uma fé infalível!",bc_map,"0xFF99CC";
	end;

OnTimer60000:
	set $@tierra01_timer, $@tierra01_timer + 1;
	if( $@tierra01_timer >= atoi(callfunc("bg_getarenainfo",1,"event_timer")) )
	{
		if( $@TierraBG1_CroixScore > $@TierraBG1_GuillScore )
		{
			set $@TierraBG1_Victory, 2;
			mapannounce "bat_a01", "Marollo VII: O Exército de Croix destruíu mais depósitos de suprimentos, é o vencedor!",bc_map,"0x99CC00";
		}
		else if( $@TierraBG1_GuillScore > $@TierraBG1_CroixScore )
		{
			set $@TierraBG1_Victory, 1;
			mapannounce "bat_a01", "Marollo VII: O Exército de Guillaume destruíu mais depósitos de suprimentos, é o vencedor!",bc_map,"0x99CC00";
		}
		else {
			set $@TierraBG1_Victory, 3;
			mapannounce "bat_a01", "Marollo VII: Ambos Exércitos não cumpriram com seus objetivos, isso é uma empate!",bc_map,"0x99CC00";
		}
		
		donpcevent "Terapeuta#a01_a::OnStop";
		donpcevent "Terapeuta#a01_b::OnStop";
		enablenpc "Tenente de Guillaume#a01_a";
		enablenpc "Tenente de Croix#a01_b";
		bg_warp $@BG_1_id1,"bat_a01",50,374;
		bg_warp $@BG_1_id2,"bat_a01",42,16;
		stopnpctimer;
	}
	else
		setnpctimer 0;
	end;
}

-	script	#tierra01_vintenar	-1,{
	callfunc("bg_rewardnpc", 1, $@BG_1_id1, $@BG_1_id2, $@TierraBG1_Victory, (strnpcinfo(2)=="a01_a"?0:1));
	end;

OnInit:
	if( strnpcinfo(2) != "tierra01_vintenar" )
		disablenpc strnpcinfo(0);
	end;
}

bat_a01,53,377,3	duplicate(#tierra01_vintenar)	Tenente de Guillaume#a01_a	4_M_KY_HEAD
bat_a01,45,19,3	duplicate(#tierra01_vintenar)	Tenente de Croix#a01_b	4_M_CRU_HEAD