//===== Venus Script =========================================
//= Funções de Navegações
//===== Por: =================================================
//= Euphy / skb_ (CreativeSD Translation)
//===== Versão atual: ========================================
//= 1.2
//===== Compatível com: ======================================
//= rAthena Project / Venus-Emulator
//===== Descrição: ===========================================
//= Controls the navigation system for Renewal guides.
//= Clients before 2011-10-10 do not support navigation
//= links, and will default to colored or plain text.
//===== Comentários Adicionais: ==============================
//= 1.0 First version.
//= 1.1 Added 'is_clientver' command. [Euphy]
//= 1.2 Dropped 'is_clientver' command. [Lemongrass]
//============================================================

function	script	F_Navi	{

	if ( PACKETVER < 20111010 )
		set .@disabled,1;

	// This function takes 0 ~ 3 parameters.
	switch(getargcount()) {
	case 0:	// Check if system is disabled.
		return .@disabled;
	case 1:	// Display information message, if enabled.
		if (!.@disabled) {
			next;
			mes getarg(0);
			mes "Quando você clicar no ^B9062F[nome do local]^000000, receberá o serviço de ^B9062FNavegação^000000 mais avançado!";
		}
		return;
	default: // Display navigation link, if enabled; else set text color, if available.
		if (!.@disabled)
			return "<NAVI>[" + getarg(0) + "]<INFO>" + getarg(1) + ",0,000,0</INFO></NAVI>";
		else if (getargcount() == 3)
			return getarg(2)+getarg(0)+"^000000";
		else
			return getarg(0);
	}
}