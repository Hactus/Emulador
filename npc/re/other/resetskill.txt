//===== Venus Script =========================================
//= Hipnotizadora
//===== Por: =================================================
//= Euphy / skb_ (CreativeSD Translation)
//===== Versão atual: ========================================
//= 1.1
//===== Compatível com: ======================================
//= rAthena Project / Venus-Emulator
//===== Descrição: ===========================================
//= [Official Conversion]
//= Stat and skill resetter for expanded first class under
//= base level 50 and normal first class.
//===== Comentários Adicionais: ==============================
//= 1.0 First version, Renewal script.
//= 1.1 Added Izlude duplicates. [Euphy]
//============================================================

// Main NPC :: resetskill_novice
//============================================================
izlude,127,175,3	script	Hipnotizadora#novice	124,{
	mes "[Hipnotizadora]";
	mes "Saudações, aventureiro.";
	mes "Eu sou membro da Academia";
	mes "de Hipnose enviado aqui para Izlude";
	mes "para fornecer serviços de Redefinição de Habilidades";
	mes "para certos personagens da Primeira Classe por um preço muito bom: grátis!";
	next;
	mes "[Hipnotizadora]";
	mes "Embora eu ofereça redefinições ilimitas";
	mes "de atributos e habilidades, no entanto";
	mes "há duas condições que devem ser";
	mes "atendidas. Primeiro você deve estar";
	mes "abaixo do ^FF0000Nível 50 de Base^000000.";
	next;
	mes "[Hipnotizadora]";
	mes "Em segundo lugar, você deve ser um";
	mes "^FF0000Espadachim, Noviço, Mago,";
	mes "Gatuno, Arqueiro, Mercador,";
	mes "Taekwon, Justiceiro ou Ninja^000000,";
	//mes "Gunslinger and Ninja^000000";
	mes "essas são as Classes qualificadas.";
	mes "Você tem alguma dúvida?";
	next;
	switch(select("^FF0000Redefinir Atributos/Habilidades?:Redefinir Atributos:Redefinir Habilidades^000000:Cancelar")) {
	case 1:
		mes "[Hipnotizadora]";
		mes "Redefinição de Atributos e Habilidades permitem";
		mes "aos aventureiros que redistribuam seus pontos de";
		mes "habilidades se estiverem insatisfeitos";
		mes "com suas habilidades atuais.";
		next;
		mes "[Hipnotizadora]";
		mes "Antes de prosseguir com a Redefinição";
		mes "de Atributos ou Habilidades, você deve";
		mes "reduzir todo o peso de todos os";
		mes "seus itens transportados em seu";
		mes "personagem para 0. Você pode colocar";
		mes "seus itens extra no Armazém Kafra.";
		next;
		mes "[Hipnotizadora]";
		mes "Ah, também é importante";
		mes "que você remova seu ^FF0000Carrinho de Mão^000000";
		mes "caso esteja com um equipado.";
		mes "Caso o contrário, a hipnose";
		mes "vai funcionar, ou vai sair pela culatra...";
		close;
	case 2:
		set .@str$,"Atributos";
	case 3:
		if (.@str$ == "")
			set .@str$,"Habilidades";
		mes "[Hipnotizadora]";
		mes "Tem certeza de que deseja";
		mes "prosseguir com meus";
		mes "serviços de ^FF0000Redefinição de "+.@str$+"^000000?";
		next;
		if(select("Sim:Cancelar") == 1)
			break;
	case 4:
		mes "[Hipnotizadora]";
		mes "Obrigado e tenha uma boa";
		mes "sorte em suas aventuras.";
		mes "Por favor, viaje em segurança~";
		close;
	}
	if ((Class >= Job_Swordman && Class <= Job_Thief) || Class == Job_Taekwon || Class == Job_Gunslinger || Class == Job_Ninja) {
		if (checkcart()) {
			mes "[Hipnotizadora]";
			mes "Ah! Por favor, remova";
			mes "seu Carrinho de Mão antes de prosseguir";
			mes "com o serviço de Redefinição de "+.@str$+".";
			mes "Obrigado por colaborar~";
			close;
		}
		if (Weight != 0) {
			mes "[Hipnotizadora]";
			mes "Se você está aqui para o meu serviço de";
			mes "Redefinição de " + .@str$ + ", por favor,";
			mes "lembre-se que você não pode";
			mes "redefinir "+.@str$+"s até que";
			mes "^FF0000o peso de seus itens transportados";
			mes "no seu Inventário seja reduzido a 0^000000.";
			next;
			mes "[Hipnotizadora]";
			mes "Por que você não coloca suas";
			mes "coisas no Armazém Kafra";
			mes "por enquanto? Dessa forma, você pode";
			mes "manter com segurança todos os seus bens.";
			close;
		}
		if (BaseLevel >= 50 && (Class == Job_Taekwon || Class == Job_Gunslinger || Class == Job_Ninja)) {
			mes "[Hipnotizadora]";
			mes "Me desculpe, mas personagens";
			mes "com Nível de Base maiores ou";
			mes "igual a 50 não são qualificados para o";
			mes "o serviço de Redefinição de " + .@str$+" que eu forneço.";
			close;
		}
		mes "[Hipnotizadora]";
		mes "Obrigado por usar";
		mes "meus serviços de Redistribuição de";
		mes @str$+". Ah e boa";
		mes "sorte para você em suas";
		mes "viagens, aventureiro.";
		if (.@str$ == "Atributos")
			resetstatus;
		else
			resetskill;
		close;
	} else {
		mes "[Hipnotizadora]";
		mes "Me desculpe, mas sua";
		mes "Classe não se qualifica";
		mes "para os serviços de Redefinição de "+.@str$;
		mes "que eu forneço. Só posso oferecer";
		mes "redefinição de "+.@str$+" para";
		mes "as seguitens Classes...";
		next;
		mes "[Hipnotizadora]";
		mes "^FF0000Espadachim, Noviço,";
		mes "Mago, Gatuno, Arqueiro,";
		mes "Mercador, Taekwon,";
		//mes "Boy, Taekwon Girl,";
		mes "Justiceiro e Ninja^000000.";
		close;
	}
}
izlude_a,127,175,3	duplicate(Hipnotizadora#novice)	Hipnotizadora#novice_a	124
izlude_b,127,175,3	duplicate(Hipnotizadora#novice)	Hipnotizadora#novice_b	124
izlude_c,127,175,3	duplicate(Hipnotizadora#novice)	Hipnotizadora#novice_c	124
izlude_d,127,175,3	duplicate(Hipnotizadora#novice)	Hipnotizadora#novice_d	124