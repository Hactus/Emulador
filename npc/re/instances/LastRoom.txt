//===== rAthena Script =======================================
//= The Last Room
//===== Description: =========================================
//= [Walkthrough Conversion]
//= The Last Room Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

un_myst,163,38,5	script	Mark	4_M_BLUEMAN,{
	mes "[Mark]";
	cutin "bu_mark1.bmp",0;

	if (BaseLevel < 150) {
		mes "É perigoso aqui.";
		mes "Seria melhor voltar quando estiver mais forte.";
		close3;
	}
	if (getcharid(1) < 1) {
		mes "...Sinto algo errado.";
		mes "Pode ser perigoso demais sozinho, forme um grupo e volte.";
		close3;
	}
	mes "Diferentemente das outras portas essa apresenta traços de estar bloqueado por este lado.";
	next;
	mes "[Mark]";
	mes "`Será que tem algo dentro?";
	next;
	switch( checkquest(11379,PLAYTIME) ) {
	case -1:
		.@party_id = getcharid(1);
		if (getpartyleader(.@party_id,2) == getcharid(0))
			.@string$ = "Open door.";
		.@s = select( .@string$, "Go in.", "Quit." );
		if (.@s == 1) {
			mes "[Mark]";
			mes "Vou abrir a porta.";
			mes "Entrem quando a portas estiver totalmente aberta.";
			instance_create("Last room");
		}
		else if (.@s == 2) {
			mes "[Mark]";
			switch( instance_enter("Last room") ) {
			case IE_NOMEMBER:
			case IE_NOINSTANCE:
			case IE_OTHER:
				mes "A porta não está abrindo direito.";
				break;
			case IE_OK:
				mapannounce "un_myst", strcharinfo(0) + " of the party, " + getpartyname(.@party_id) + ", is entering Last room.",bc_map,"0x00FF99";
				setquest 11379;// Final Room
				if (isbegin_quest(11380) == 0)
					setquest 11380;// Final Room
				break;
			}
		}
		close3;
	case 0:
	case 1:
		mes "[Mark]";
		cutin "bu_mark1.bmp",0;
		mes "...Parecem cansados.";
		mes "Tirei um dia de folga antes de entrarem novamente.";
		close3;
	case 2:
		mes "[Mark]";
		cutin "bu_mark1.bmp",0;
		mes "Não acredito que queiram entrar aí novamento...";
		erasequest 11379;// Final Room
		close3;
	}
}

// 1st step
1@uns,139,41,3	script	Mark#room1	4_M_BLUEMAN,{
	mes "[Mark]";
	mes "Algo parece errado, melhor irmos com cautela.";
	cutin "bu_mark4.bmp",0;
	close3;
}

1@uns,145,32,5	script	Alp#room1	4_M_BLACKMAN,{
	mes "[Alp]";
	mes "Há traços supeitos em todo lugar.";
	mes "Parece que um maquinário pesado passou por aqui...";
	cutin "bu_alp1.bmp",2;
	close3;
}

1@uns,145,37,3	script	Tamarin#room1	4_M_TAMARIN,{
	mes "[Tamarin]";
	mes "Verity, você está bem?";
	mes "Não me parece muito bem...";
	cutin "ep143_taang.bmp",2;
	close3;
}

1@uns,143,38,5	script	Du#room1	4_M_REDMAN,{
	mes "[Du]";
	mes "Estou com um mal pressentimento...";
	cutin "bu_du1.bmp",2;
	close3;
}

1@uns,143,39,5	script	Maggi#room1	4_F_PINKWOMAN,{
	mes "[Maggi]";
	mes "Aahh....";
	cutin "bu_maggi4.bmp",2;
	close3;
}

1@uns,144,37,3	script	Verity#room1	4_F_BERRYTEA,{
	if ('protocole < 2) {
		mes "[Verity]";
		mes "Umph...mudei de idéia podemos voltar? Algo está errado.";
		cutin "EP15_2_brt_6.bmp",2;
		if (getpartyleader(getcharid(1),2) != getcharid(0)) {
			mes "Desejo falar com o líder.";
			close3;
		}
		npctalk "Verity: Umph...mudei de idéia podemos voltar? Algo está errado.";
		next;
		mes "[Verity]";
		mes "Minha cabeça parece que vai explodir";
		npctalk "Verity: Minha cabeça parece que vai explodir.";
		next;
		mes "[Verity]";
		mes "....Ugh....";
		npctalk "Verity: ....Ugh....";
		next;
		mes "[Du]";
		mes "Irmã! você está bem?!";
		cutin "bu_du5.bmp",2;
		npctalk "Du: Irmã! você está bem?!", instance_npcname("Du#room1");
		next;
		mes "[Verity]";
		mes "....Temos de voltar....";
		cutin "EP15_2_brt_7.bmp",2;
		npctalk "Verity: ....Temos de voltar....";
		next;
		mes "[Tamarin]";
		mes "Verity, você está bem?";
		mes "Consegue andar?";
		cutin "ep143_tahuk.bmp",2;
		npctalk "Tamarin: Verity, você está bem? Consegue andar?", instance_npcname("Tamarin#room1");
		next;
		mes "[Verity]";
		mes "Ugh...";
		cutin "EP15_2_brt_6.bmp",2;
		npctalk "Verity: Ugh...";
		next;
		mes "[Du]";
		mes "" + strcharinfo(0) + "! Acho melhor você voltar.";
		cutin "bu_du5.bmp",2;
		npctalk "Du: " + strcharinfo(0) + "! Acho melhor você voltar.", instance_npcname("Du#room1");
		next;
		mes "[Verity]";
		mes "N...não. Vão em frente, já alcanço vocês.";
		cutin "EP15_2_brt_4.bmp",2;
		npctalk "Verity: N...não. Vão em frente, já alcanço vocês.";
		next;
		mes "[Tamarin]";
		mes "Eu ficarei com Verity até ela melhorar, sigam em frente.";
		cutin "ep143_tahuk.bmp",2;
		npctalk "Tamarin: Eu ficarei com Verity até ela melhorar, sigam em frente.";
		close2;
		'protocole = 1;
		cutin "",255;
	}
	end;
}

1@uns,140,45,0	script	#lrdoor2	HIDDEN_WARP_NPC,6,6,{
	end;
OnTouch:
	if ('protocole == 0)
		warp 'map_name$,142,30;
	else if ('protocole == 1) {
		'protocole = 2;
		disablenpc instance_npcname("#lrdoor2");
		disablenpc instance_npcname("Mark#room1");
		disablenpc instance_npcname("Du#room1");
		disablenpc instance_npcname("Tamarin#room1");
		disablenpc instance_npcname("Verity#room1");
		disablenpc instance_npcname("Maggi#room1");
		disablenpc instance_npcname("Alp#room1");
		mapannounce 'map_name$, "-----------------Creak---------------------",bc_map,"0xFF0000";
		sleep 2000;
		mapannounce 'map_name$, "-----Creak---Wrr--------------------------",bc_map,"0xFF0000";
		sleep 2000;
		mapannounce 'map_name$, "--------------Wirrrrrrr-----------------",bc_map,"0xFF0000";
		sleep 2000;
		mapannounce 'map_name$, "-----Zizip--------Zizizip------------------",bc_map,"0xFF0000";
		sleep 2000;
		mapannounce 'map_name$, "Mensagem do Sitema : Intrusos detectados.",bc_map,"0xFF0000";
		sleep 2000;
		mapannounce 'map_name$, "Mensagem do Sistema : Fechando saídas e elimando intrusos de acordo com o protocolo.",bc_map,"0xFF0000";
		donpcevent instance_npcname("protocole1") + "::OnStart";
	}
	end;
}

1@uns,1,1,0	script	protocole1	-1,{
	end;
OnStart:
	enablenpc instance_npcname("protocole1");
	monster 'map_name$,47,49,"System message",3253,1, instance_npcname("protocole1") + "::OnMyMobDead2";
	callsub S_Spawn;
OnTimer300000:
	mapannounce 'map_name$, "Mensagem do Sistema : Eliminando intruso.",bc_map,"0xFF0000";
	callsub S_Spawn;
S_Spawn:
	initnpctimer;
	.@npc_name$ = instance_npcname("protocole1");
	// don't kill System message
	killmonster 'map_name$, .@npc_name$ + "::OnMyMobDead";
	areamonster 'map_name$,118,56,131,67, "Machine Component",3251,6, .@npc_name$ + "::OnMyMobDead";
	areamonster 'map_name$, 62,82, 61,67, "Machine Component",3251,6, .@npc_name$ + "::OnMyMobDead";
	areamonster 'map_name$, 46,89, 64,96, "Machine Component",3251,6, .@npc_name$ + "::OnMyMobDead";
	areamonster 'map_name$,118,56,131,67, "Machine Component",3252,6, .@npc_name$ + "::OnMyMobDead";
	areamonster 'map_name$, 62,82, 61,67, "Machine Component",3252,6, .@npc_name$ + "::OnMyMobDead";
	areamonster 'map_name$, 46,89, 64,96, "Machine Component",3252,6, .@npc_name$ + "::OnMyMobDead";
	end;
OnMyMobDead:
OnMyMobDead2:
	.@count = mobcount( 'map_name$, instance_npcname("protocole1") + "::OnMyMobDead" ) + mobcount( 'map_name$, instance_npcname("protocole1") + "::OnMyMobDead2" );
	if (.@count < 1) {
		mapannounce 'map_name$, "Mensagem do Sistema : Primero protocolo falhou. Iniciando próximo protocolo.",bc_map,"0xFF0000";
		stopnpctimer;
		enablenpc instance_npcname("Tamarin#room2");
		disablenpc instance_npcname("protocole1");
	}
	end;
}

1@uns,67,97,3	script	Tamarin#room2	4_M_TAMARIN,{
	mes "[Tamarin]";
	mes "Huff huff... Verity de repente huff huff não conseguia ficar em pé huff huff...";
	cutin "ep143_tahuk.bmp",2;
	if (getpartyleader(getcharid(1),2) != getcharid(0)) {
		next;
		mes "[Tamarin]";
		mes "Eu não acho que isto é um problema que possamos resolver conversando.";
		mes "Vamos pedir a opinião do líder primeiro.";
		close3;
	}
	npctalk "Tamarin: Huff huff... Verity de repente huff huff não conseguia ficar em pé huff huff...";
	next;
	mes "[Tamarin]";
	mes "Saiu correndo tão rápido huff...huff... não consegui alcança-lá. Huff huff";
	npctalk "Tamarin: Saiu correndo tão rápido huff...huff... não consegui alcança-lá. Huff huff";
	next;
	mapannounce 'map_name$, "Mensagem do Sistema : Objeto não identificado se movendo rápidamente da Zona 1 para Zona 2.",bc_map,"0xFF0000";
	mes "[Tamarin]";
	mes "Uh... será que é a Verity?";
	npctalk "Tamarin: Uh... será que é a Verity?";
	next;
	mapannounce 'map_name$, "Mensagem do Sistema : Iniciando protocolo de emergência.",bc_map,"0xFF0000";
	mes "[Tamarin]";
	mes "Temos de achar Verity rápido";
	npctalk "Tamarin: Temos de achar Verity rápido";
	next;
	mes "[Tamarin]";
	mes "Só existe uma rota para subir portanto vão em frente.";
	npctalk "Tamarin: Só existe uma rota para subir portanto vão em frente.";
	close2;
	if ('protocole == 2) {
		'protocole = 3;
		enablenpc instance_npcname("#lrdoor4");
		for ( .@i = 1; .@i < 16; ++.@i )
			enablenpc instance_npcname( "#lrboom" + .@i );
		enablenpc instance_npcname("Verity#room2");
		enablenpc instance_npcname("Verity#room3");
	}
	cutin "",255;
	end;
}

// 2nd step
1@uns,100,121,0	warp2	#lrdoor4	3,3,1@uns,145,107

1@uns,157,94,0	script	#lrboom1	4_CRACK,2,2,{
	end;
OnTouch:
	.@npc_name$ = instance_npcname( strnpcinfo(0) );
	areamonster 'map_name$,176,26,196,52, "Machine Component", (3251 + rand(2)), 3, .@npc_name$ + "::OnMyMobDead";
	mapannounce 'map_name$, "Mensagem do Sistema : Inruso detectado na Zona 2.",bc_map,"0xFF0000";
	disablenpc .@npc_name$;
	end;
OnMyMobDead:
	end;
}
1@uns,164,97,0	duplicate(#lrboom1)	#lrboom2	4_CRACK,2,2
1@uns,166,92,0	duplicate(#lrboom1)	#lrboom3	4_CRACK,2,2
1@uns,170,96,0	duplicate(#lrboom1)	#lrboom4	4_CRACK,2,2
1@uns,175,93,0	duplicate(#lrboom1)	#lrboom5	4_CRACK,2,2
1@uns,190,97,0	duplicate(#lrboom1)	#lrboom6	4_CRACK,2,2
1@uns,194,94,0	duplicate(#lrboom1)	#lrboom7	4_CRACK,2,2
1@uns,199,96,0	duplicate(#lrboom1)	#lrboom8	4_CRACK,2,2
1@uns,206,93,0	duplicate(#lrboom1)	#lrboom9	4_CRACK,2,2
1@uns,217,91,0	duplicate(#lrboom1)	#lrboom10	4_CRACK,2,2
1@uns,224,79,0	duplicate(#lrboom1)	#lrboom11	4_CRACK,2,2
1@uns,227,73,0	duplicate(#lrboom1)	#lrboom12	4_CRACK,2,2
1@uns,227,69,0	duplicate(#lrboom1)	#lrboom13	4_CRACK,2,2
1@uns,220,63,0	duplicate(#lrboom1)	#lrboom14	4_CRACK,2,2
1@uns,201,60,0	duplicate(#lrboom1)	#lrboom15	4_CRACK,2,2

1@uns,224,29,3	script	Verity#room2	4_F_BERRYTEA,{
	mes "[Verity]";
	mes "Ah...onde estou?..";
	mes "Oque aconteceu?";
	cutin "EP15_2_brt_6.bmp",2;
	next;
	mes "[Verity]";
	mes "Minha cabeça... parece que vai explodir.";
	mes "Tem um som vindo... desta porta....";
	next;
	if (select( "Sair.", "Entrar." ) == 1) {
		mes "[Verity]";
		mes "Voltar... temos de voltar...";
		cutin "EP15_2_brt_7.bmp",2;
		close3;
	}
	mes "[Verity]";
	mes "Rápido... temos de voltar...";
	cutin "EP15_2_brt_7.bmp",2;
	close2;
	warp 'map_name$,240,255;
	cutin "",255;
	end;
}

// Last step
1@uns,242,253,3	script	Verity#room3	4_F_BERRYTEA,{
	mes "[Verity]";
	mes "Finalmente... voltamos...";
	mes "Finalmente.....";
	cutin "EP15_2_brt_7.bmp",2;
	close2;
	cutin "",255;
	if ('protocole == 3) {
		'protocole = 4;
		disablenpc instance_npcname("Verity#room3");
		donpcevent instance_npcname("protocole3") + "::OnStart";
	}
	end;
}

1@uns,1,1,0	script	protocole3	-1,{
	end;
OnStart:
	enablenpc instance_npcname("protocole3");
	initnpctimer;
	mapannounce 'map_name$, "Mensagem do Sistema : Intruso detectado na sala mãe.",bc_map,"0xFF0000";
	monster 'map_name$,247,270,"T_W_O",3254,1, instance_npcname("protocole3") + "::OnMyMobDead";
	end;
OnTimer2000:
	mapannounce 'map_name$, "Mensagem do Sistema : Começando protocolo final.",bc_map,"0xFF0000";
	end;
OnTimer4000:
	mapannounce 'map_name$, "Mensagem do Sistema : Alterando todos os sistemas de modo manutenção para modo de combate.",bc_map,"0xFF0000";
	end;
OnTimer6000:
	mapannounce 'map_name$, "Mensagem do Sistema : Este é o último protocolo protetor.... pessoas não autorizadas por favor evacuem o lugar.",bc_map,"0xFF0000";
	end;
OnTimer9000:
	mapannounce 'map_name$, "T_W_O : Temos de voltar.... este não é o lugar certo....",bc_map,"0xFF0000";
	end;
OnTimer28000:
	mapannounce 'map_name$, "Mensagem do Sistema : O sistema foi danificado.",bc_map,"0xFF0000";
	areamonster 'map_name$,222,250,267,281, "System message",3253,3, instance_npcname("protocole3") + "::OnMyMobDead2";
	end;
OnTimer30000:
	mapannounce 'map_name$, "T_W_O : Quem sou eu? Porque estou aqui?.",bc_map,"0xFF0000";
	end;
OnTimer58000:
	mapannounce 'map_name$, "Mensagem do Sistema : Pode haver zonas criticamente danificadas dentro da zona em que o dano ocorreu, tome cuidado.",bc_map,"0xFF0000";
	end;
OnTimer88000:
	mapannounce 'map_name$, "Mensagem do Sistema : O sistema foi danificado.",bc_map,"0xFF0000";
	areamonster 'map_name$,222,250,267,281, "System message",3253,3, instance_npcname("protocole3") + "::OnMyMobDead2";
	end;
OnTimer98000:
	mapannounce 'map_name$, "T_W_O : Não resta nada....",bc_map,"0xFF0000";
	end;
OnTimer108000:
	mapannounce 'map_name$, "T_W_O : Nem eu... nem eles...",bc_map,"0xFF0000";
	end;
OnTimer148000:
	mapannounce 'map_name$, "Mensagem do Sistema : O sistema foi danificado.",bc_map,"0xFF0000";
	areamonster 'map_name$,222,250,267,281, "System message",3253,3, instance_npcname("protocole3") + "::OnMyMobDead2";
	end;
OnTimer208000:
	mapannounce 'map_name$, "Mensagem do Sistema : O sistema foi danificado.",bc_map,"0xFF0000";
	areamonster 'map_name$,222,250,267,281, "System message",3253,3, instance_npcname("protocole3") + "::OnMyMobDead2";
	stopnpctimer;
	end;
OnMyMobDead:
	stopnpctimer;
	killmonster 'map_name$, instance_npcname("protocole3") + "::OnMyMobDead2";
	mapannounce 'map_name$, "Mensagem do Sistema : Todos os protocolos falharam. Isolando área para excluir a ameaça.",bc_map,"0xFF0000";
	enablenpc instance_npcname("Alp#room4");
	enablenpc instance_npcname("Tamarin#room4");
	enablenpc instance_npcname("Maggi#room4");
	enablenpc instance_npcname("Du#room4");
	enablenpc instance_npcname("Mark#room4");
	enablenpc instance_npcname("Verity#room4");
	disablenpc instance_npcname("protocole3");
	end;
OnMyMobDead2:
	end;
}
	
1@uns,236,258,1	script	Alp#room4	4_M_BLACKMAN,{
	mes "[Alp]";
	mes "Cedo demais para relaxarmos...";
	cutin "bu_alp1.bmp",2;
	close3;
}

1@uns,244,257,3	script	Tamarin#room4	4_M_TAMARIN,{
	mes "[Tamarin]";
	mes "Verity!!!";
	mes "Recomponha-se!";
	cutin "ep143_tahuk.bmp",2;
	close3;
}

1@uns,245,254,3	script	Maggi#room4	4_F_PINKWOMAN,{
	mes "[Maggi]";
	mes "Verity, você está bem?";
	cutin "bu_maggi4.bmp",2;
	close3;
}

1@uns,244,254,3	script	Du#room4	4_M_REDMAN,{
	mes "[Du]";
	mes "Irmã!";
	mes "Oque aconteceu?!";
	mes "Tudo bem com você?";
	cutin "bu_du5.bmp",2;
	close3;
}

1@uns,242,255,5	script	Mark#room4	4_M_BLUEMAN,{
	mes "[Mark]";
	mes "Saiam da Frente!";
	mes "Eu irei curá-la!!!";
	cutin "bu_mark4.bmp",0;
	close3;
}

1@uns,242,253,3	script	Verity#room4	4_F_BERRYTEA,{
	mes "[Du]";
	mes "Irmã!";
	mes "Oque aconteceu?!";
	mes "Tudo bem com você?";
	cutin "bu_du5.bmp",2;
	npctalk "Du: Irmã! Oque aconteceu?! Tudo bem com você?", instance_npcname("Du#room4");
	next;
	mes "[Tamarin]";
	mes "Verity!!!";
	mes "Recomponha-se!";
	cutin "ep143_tahuk.bmp",2;
	npctalk "Tamarin: Verity!!! Recomponha-se!", instance_npcname("Tamarin#room4");
	next;
	mes "[Mark]";
	mes "Saiam da frente!";
	mes "Eu irei curá-la!!!";
	cutin "bu_mark4.bmp",0;
	npctalk "Mark: Saiam da frente! Eu irei curá-la!!!", instance_npcname("Mark#room4");
	next;
	mes "[Verity]";
	mes "Uh..uhm...";
	cutin "EP15_2_brt_4.bmp",2;
	npctalk "Verity: Uh..uhm...";
	next;
	mes "[Verity]";
	mes "Oh...";
	mes "Oque está acontecendo?.";
	mes "Porque essas caras sérias?.";
	cutin "EP15_2_brt_5.bmp",2;
	npctalk "Verity Oh... Oque está acontecendo. Porque essas caras sérias.";
	next;
	mes "[Du]";
	mes "O que você acha!";
	mes "Não lembra?";
	cutin "bu_du4.bmp",2;
	npctalk "Du: O que você acha! Não lembra?", instance_npcname("Du#room4");
	next;
	mes "[Verity]";
	mes "Lembrar?";
	cutin "EP15_2_brt_5.bmp",2;
	npctalk "Verity: Lembrar?";
	next;
	mes "[Verity]";
	mes "... Parando para prensar, aonde estou?";
	cutin "EP15_2_brt_3.bmp",2;
	npctalk "Verity: ... Parando para prensar, aonde estou?";
	next;
	mes "[Tamarin]";
	mes "Verity você não se recorda de nada?";
	mes "Antes...";
	cutin "ep143_tahuk.bmp",2;
	npctalk "Tamarin: Verity você não se recorda de nada? Antes...", instance_npcname("Tamarin#room4");
	next;
	mes "[Verity]";
	mes "Uhm...";
	mes "Minha cabeça estava doendo muito, parecia que ia rachar ao meio desde que fui pro subsolo...";
	npctalk "Verity: Minha cabeça estava doendo muito, parecia que ia rachar ao meio desde que fui pro subsolo...";
	cutin "EP15_2_brt_3.bmp",2;
	next;
	mes "[Verity]";
	mes "Não lembro de nada depois disso.";
	npctalk "Verity: Não lembro de nada depois disso.";
	cutin "EP15_2_brt_5.bmp",2;
	next;
	mes "[Du]";
	mes "Neste instante algo tremendo...";
	cutin "bu_du4.bmp",2;
	npctalk "Du: Neste instante algo temendro...", instance_npcname("Du#room4");
	next;
	mes "[Alp]";
	mes "Ei... Não acho que agora seja a melhor situação para ficarmos conversando.";
	cutin "bu_alp3.bmp",2;
	npctalk "Alp: Ei... Não acho que agora seja a melhor situação para ficarmos conversando.", instance_npcname("Alp#room4");
	next;
	mes "[Du]";
	mes "Isto é conversar para você?";
	mes "Antes disso aconteceu algo tremendo, ok?";
	cutin "bu_du4.bmp",2;
	npctalk "Du: Isto é conversar para você? It Antes disso aconteceu algo tremendo, ok?", instance_npcname("Du#room4");
	next;
	mes "[Alp]";
	mes "Estão ouvindo isso?";
	mes "Tem algo se aproximando...";
	cutin "bu_alp1.bmp",2;
	npctalk "Alp: Estão ouvindo isso? Tem algo se aproximando...", instance_npcname("Alp#room4");
	next;
	mes "[Tamarin]";
	mes "Agora... acabei de reparar que o chão.... está tremendo um pouco, não é?";
	cutin "ep143_tahuk.bmp",2;
	npctalk "Tamarin: Agora... acabei de reparar que o chão.... está tremendo um pouco, não é?", instance_npcname("Tamarin#room4");
	next;
	mes "[Du]";
	mes "Oque... oque é isso?";
	cutin "bu_du4.bmp",2;
	npctalk "Du: Oque... oque é isso?", instance_npcname("Du#room4");
	next;
	mes "[Alp]";
	mes "Isto é... perigoso.";
	mes "É muita areia pro nosso caminhãozinho...";
	cutin "bu_alp2.bmp",2;
	npctalk "Alp: Isto é... perigoso. É muita areia pro nosso caminhãozinho...", instance_npcname("Alp#room4");
	next;
	mes "[Mark]";
	mes "Rá...rápido vamos sair daqui!!!!!";
	cutin "bu_mark4.bmp",0;
	npctalk "Mark: Rá...rápido vamos sair daqui!!!!!", instance_npcname("Mark#room4");
	close2;
	warp "verus01",151,173;
	cutin "",255;
	end;

OnInstanceInit:
	'map_name$ = instance_mapname("1@uns");
	'protocole = 0;

	// 1st step
	disablenpc instance_npcname("protocole1");
	disablenpc instance_npcname("Tamarin#room2");
	disablenpc instance_npcname("#lrdoor4");

	// 2nd step
	disablenpc instance_npcname("Verity#room2");
	for ( .@i = 1; .@i < 16; ++.@i )
		disablenpc instance_npcname( "#lrboom" + .@i );

	// Last step
	disablenpc instance_npcname("Verity#room3");
	disablenpc instance_npcname("protocole3");
	disablenpc instance_npcname("Alp#room4");
	disablenpc instance_npcname("Tamarin#room4");
	disablenpc instance_npcname("Maggi#room4");
	disablenpc instance_npcname("Du#room4");
	disablenpc instance_npcname("Mark#room4");
	disablenpc instance_npcname("Verity#room4");
	end;
}