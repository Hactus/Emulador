//===== rAthena Script =======================================
//= Enchants NPCs.
//===== Description: =========================================
//= Temporal Boots enchants NPCs.
//===== Changelogs: ==========================================
//= 1.0 First Version merged from idathena.
//=     Credit exneval. [Capuche]
//============================================================

// Merchant
//============================================================
glast_01,210,273,5	script	Mordomo de Hugin#pa0829	1_F_04,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "Sua mochila está sobrecarregada, esvazie-a um pouco e volte.";
		close;
	}
	mes "[Mordomo de Hugin]";
	mes "Traga vários do item Cristal Temporal para trocar por bota temporais. Escolha algo que queira.";
	next;
	switch( select( "Cancelar", "Comprar Bota Temporal (necessita 1 Cristal Temporal))", "Aprimorar Bota temporal (Necessita 5 Cristal Temporal)", "Aprimorar Botas Modificadas (Necessita 5 Cristal Temporal)" ) ) {
	case 1:
		mes "[Mordomo de Hugin]";
		mes "Volte sempre que quiser.";
		close;
	case 2:
		if (countitem(6607) > 0) {
			mes "[Mordomo de Hugin]";
			mes "Aqui está sua Bota Temporal. Podemos aprimorá-la por 5 Cristais Temporais.";
			delitem 6607,1;// Temporal_Crystal
			getitem 2499,1;// Temporal_Boots
			close;
		}
		mes "[Mordomo de Hugin]";
		mes "Necessita mais Cristais Temporais. Volte quando conseguir mais Cristais Temporais em sua exploração da Velha Glast Heim.";
		close;
	case 3:
		setarray .@equip_type[0],
			22000,	// Temporal_STR_Boots
			22001,	// Temporal_INT_Boots
			22002,	// Temporal_AGI_Boots
			22003,	// Temporal_VIT_Boots
			22004,	// Temporal_DEX_Boots
			22005;	// Temporal_LUK_Boots
		break;
	case 4:
		setarray .@equip_type[0],
			22107,	// Modified_STR_Boots
			22108,	// Modified_INT_Boots
			22109,	// Modified_AGI_Boots
			22110,	// Modified_VIT_Boots
			22111,	// Modified_DEX_Boots
			22112;	// Modified_LUK_Boots
		break;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[Mordomo de Hugin]";
		mes "Apenas o item que estiver utilizando pode ser aprimorado";
		mes "Calce a ^0000FFBota Temporal^000000 que deseja aprimorar, e volte a falar comigo..";
		close;
	}
	if (getequipid(EQI_SHOES) != 2499) {
		mes "[Mordomo de Hugin]";
		mes "Você não está utilizado a Bota Temporal. Fale comigo novamente quando a estiver utilizando.";
		close;
	}
	if (countitem(6607) < 5) {
		mes "[Mordomo de Hugin]";
		mes "Faltam Critais Temporais. Necessito de 5 Cristais Temporais para aprimorar seu item.";
		close;
	}
	mes "[Mordomo de Hugin]";
	mes "Você pode trocar ^0000FF1 Bota Temporal e 5 Cristai Temporal^000000 por 1 dos seguintes items. ^FF0000Aprimoramente anterior atual do item não será usado no novo item^000000.";
	next;
	.@s = select( "Cancelar", "Botas STR", "Botas INT", "Botas AGI", "Botas VIT", "Botas DEX", "Botas Luk" ) - 2;
	if (.@s == -1) {
		mes "[Mordomo de Hugin]";
		mes "Volte quando quiser algo ^^.";
		close;
	}
	setarray .@type$[0], "Strength", "Intelligence", "Agility", "Vitality", "Dexterity", "Lucky";
	mes "[Mordomo de Hugin]";
	mes "Tem certeza que quer comprar Bota de ^FF0000" + .@type$[.@s] + "^000000 Temporal? Não há devoluções.";
	next;
	if (select( "Repensar", "Tenho Certeza." ) == 1) {
		mes "[Mordomo de Hugin]";
		mes "Ok, volte quando tiver certeza.";
		close;
	}
	mes "[Mordomo de Hugin]";
	mes "As botas foram aprimoradas como você pediu.";
	delequip EQI_SHOES;
	delitem 6607,5;// Temporal_Crystal
	getitem .@equip_type[.@s],1;
	close;
}

// Enchant and Socket NPC
//============================================================
glast_01,212,273,4	script	Mestre Mágico de Hugin#pa0829	1_F_01,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "Sua mochila está sobrecarregada, esvazie-a um pouco e volte.";
		close;
	}
	mes "[Mestre Mágico de Hugin]";
	mes "Então veio encantar sua bota temporal. Esteja ciente que botas com slot não podem ser encantadas, e este encantamento é apenas para a bota que estiver equipada.";
	next;
	if (select( "Como funciona o encantamento?", "Encante minhas Botas Temporais" ) == 1) {
		mes "[Mestre Mágico de Hugin]";
		mes "Você pode obter ^0000FFMagia Coagulada^000000 atráves de suas explorações.";
		next;
		mes "[Mestre Mágico de Hugin]";
		mes "Eu posso encantar suas botas temporais dependendo de quanto deste item. ^0000FFImpossível encantar botas nao aprimoradas^000000. Apenas as 6 versões aprimoradas.";
		next;
		mes "[Mestre Mágico de Hugin]";
		mes "Mas, se você slotar sua bota, não poderei encantá-la. Lembre-se disso.";
		next;
		mes "[Mestre Mágico de Hugin]";
		mes "O efeito não será aleatório. Você pode escolher o encantamento de sua preferência.";
		next;
		mes "[Mestre Mágico de Hugin]";
		mes "Apenas para o 4º slot claro. Você terá um encantamento pequeno no começo, e conforme você encanta ficará mais forte.";
		next;
		mes "[Mestre Mágico de Hugin]";
		mes "Para chegar no aprimoramente final, necessita-se de 50 magia coagulada. Não se esqueça.";
		next;
		mes "[Mestre Mágico de Hugin]";
		mes "Quando o 4º encantamento chegar ao máxinmo, você pode ganhar um efeito aleatório no 3º slot. Com certo... pagamento~";
		next;
		mes "[Mestre Mágico de Hugin]";
		mes "A propósito, você não perderá o item se o encantamento falhar, cartas e refino também não serão perdidos. Mas não tem como desencantar...";
		next;
		mes "[Mestre Mágico de Hugin]";
		mes "Ufa...";
		mes "Enfim, isso é todo o básico que precisa saber. Agora veja por você mesmo.";
		close;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[Mestre Mágico de Hugin]";
		mes "Está utilizando a bota a ser encantada?";
		close;
	}
	.@equip_id = getequipid(EQI_SHOES);

	setarray .@enchant_1[0],4808,4832,4814,4741,4869,4752;// Fighting_Spirit4	Expert_Archer1	Spell2	Vitality2	DelayafterAttack1Lv	Luck3
	setarray .@enchant_2[0],4820,4833,4813,4742,4872,4753;// Fighting_Spirit5	Expert_Archer2	Spell3	Vitality3	DelayafterAttack2Lv	Luck4
	setarray .@enchant_3[0],4821,4834,4812,4861,4873,4754;// Fighting_Spirit6	Expert_Archer3	Spell4	MHP1		DelayafterAttack3Lv	Luck5
	setarray .@enchant_4[0],4822,4835,4826,4862,4881,4755;// Fighting_Spirit7	Expert_Archer4	Spell5	MHP2		DelayafterAttack4Lv	Luck6
	setarray .@enchant_cost[0],1,4,15,30,10;

	switch(.@equip_id) {
	case 22000:	// Temporal_STR_Boots
	case 22001:	// Temporal_INT_Boots
	case 22002:	// Temporal_AGI_Boots
	case 22003:	// Temporal_VIT_Boots
	case 22004:	// Temporal_DEX_Boots
	case 22005:	// Temporal_LUK_Boots

	case 22107:	// Modified_STR_Boots
	case 22108:	// Modified_INT_Boots
	case 22109:	// Modified_AGI_Boots
	case 22110:	// Modified_VIT_Boots
	case 22111:	// Modified_DEX_Boots
	case 22112:	// Modified_LUK_Boots
		.@equip_name$ = getequipname(EQI_SHOES);
		.@card2 = getequipcardid(EQI_SHOES,2);
		.@card3 = getequipcardid(EQI_SHOES,3);
		.@equip_refine = getequiprefinerycnt(EQI_SHOES);

		if (.@card2 > 0) {
			mes "[Mestre Mágico de Hugin]";
			mes "Essa bota já está encantanda ao limite. Não tem como aumentar.";
			close;
		}
		if (.@card3 == 0) {// 4th slot 1st try enchanting
			.@cost = .@enchant_cost[0];
			mes "[Mestre Mágico de Hugin]";
			mes "Deseja encantar ^0000FF" + .@equip_name$ + "^000000?";
			mes "Para o 1º encantamento, necessito de ^0000FF" + .@cost + "^000000 Magias Coaguladas.";
			next;
			.@s = select( "Sair", "Espírito do Lutador", "Mira Apurada", "Pedra de Conjuração", "Vitalidade", "ASPD", "Sorte" ) - 2;
			if (.@s == -1) {
				mes "[Mestre Mágico de Hugin]";
				mes "Ok, volte quando estiver decidido.";
				close;
			}
			.@card3 = .@enchant_1[.@s];
			.@string$ = "enchant number ^6300001^000000.";
		}
		else {
			for ( .@enchant_num = 1; .@enchant_num < 5; .@enchant_num++ ) {
				for ( .@enchant_type = 0; .@enchant_type < 6 && .@card3 != getd( ".@enchant_" + .@enchant_num + "[" + .@enchant_type + "]" ); .@enchant_type++ )
					continue;
				if (.@enchant_type < 6)
					break;
			}
			if (.@enchant_num == 5) {
				mes "[Mestre Mágico de Hugin]";
				mes "Aconteceu algo errado.";
				close;
			}
			.@cost = .@enchant_cost[.@enchant_num];

			mes "[Mestre Mágico de Hugin]";
			if (.@enchant_num == 4) {
				.@card2 = callfunc("F_Rand",4875,4876,4877,4878,4879,4880);// Bear's_Power, Runaway_Magic, Speed_Of_Light, Muscle_Fool, Hawkeye, Lucky_Day
				.@string$ = "^990000Bonus effect ^000000 upgrade.";
				mes "4º slot está no limite de encantamneto. Deseja um encantamento aleatório no 3º slot? Necessito de ^0000ff" + .@cost + "^000000 magia coagulada.";
			}
			else {
				.@number = .@enchant_num + 1;
				.@card3 = getd( ".@enchant_" + (.@enchant_num+1) + "[" + .@enchant_type + "]" );
				.@string$ = "enchant number ^630000" + .@number + "^000000.";
				mes "Encantando ^0000FF" + .@equip_name$ + "^000000's 4º slot com ^0000FF" + .@number + "^000000 efeito. necessita ^0000FF" + .@cost + "^000000 Magia Coagulada.";
			}
			next;
			if (select("Sair","Aprimorar Encantamento!") == 1) {
				mes "[Mestre Mágico de Hugin]";
				mes "Ok, Volte quando estiver pronto.";
				close;
			}
		}
		if (countitem(6608) < .@cost) {
			mes "[Mestre Mágico de Hugin]";
			mes "Hmm, está faltando " + (.@cost - countitem(6608)) + " Magia Coagulada. Junte mais e poderemos conversar sobre encantamentos.";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		delitem 6608,.@cost;// Coagulated_Spell
		delequip EQI_SHOES;
		mes "[Mestre Mágico de Hugin]";
		mes "Trying for " + .@string$;
		getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,.@card2,.@card3;
		close;
	default:
		mes "[Mestre Mágico de Hugin]";
		mes "Este não é o item correto a ser encantado.";
		mes "Lembre-se, apenas as 6 botas aprimoradas podem ser encantadas.";
		close;
	}
}

glast_01,210,270,3	script	Artesão de Hugin#pa0829	4_F_JOB_BLACKSMITH,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "Sua mochila está sobrecarregada, esvazie-a um pouco e volte.";
		close;
	}
	mes "[Artesão de Hugin]";
	mes "O mestre mágico diz q encantar Botas Temporais sempre será a melhor opção. Mas, eu discordo.";
	next;
	mes "[Artesão de Hugin]";
	mes "Acredito que adicionar um slot ao item é o ideal. Mesmo tendo chance de falhar.";
	next;
	mes "[Artesão de Hugin]";
	mes "Traga-me ^0000FF5 Temporal Crystals^000000, e tentarei adicionar um slot a suas botas temporais. Mas, é possível ^FF0000falhar e quebrar o item^000000. Quer arriscar?";
	next;
	if (select( "Cancelar", "Arriscar" ) == 1) {
		mes "[Artesão de Hugin]";
		mes "Ok, volte quando quiser.";
		close;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[Artesão de Hugin]";
		mes "Tem certeza que está utilizando o item certo?";
		close;
	}
	.@equip_id = getequipid(EQI_SHOES);
	switch(.@equip_id) {
	case 22000:	callsub( S_Slot,22006 );	// Temporal_STR_Boots
	case 22001:	callsub( S_Slot,22009 );	// Temporal_INT_Boots
	case 22002:	callsub( S_Slot,22010 );	// Temporal_AGI_Boots
	case 22003:	callsub( S_Slot,22007 );	// Temporal_VIT_Boots
	case 22004:	callsub( S_Slot,22008 );	// Temporal_DEX_Boots
	case 22005:	callsub( S_Slot,22011 );	// Temporal_LUK_Boots

	case 22107:	callsub( S_Slot,22113 );	// Modified_STR_Boots
	case 22108:	callsub( S_Slot,22114 );	// Modified_INT_Boots
	case 22109:	callsub( S_Slot,22115 );	// Modified_AGI_Boots
	case 22110:	callsub( S_Slot,22116 );	// Modified_VIT_Boots
	case 22111:	callsub( S_Slot,22117 );	// Modified_DEX_Boots
	case 22112:	callsub( S_Slot,22118 );	// Modified_LUK_Boots
	default:
		mes "[Artesão de Hugin]";
		mes "Não é este! Preciso de ^Botas Temporais Avançada^000000 items! O item ^0000FFcom efeito de atributo e sem slot^000000!";
		close;
	}

S_Slot:
	mes "[Artesão de Hugin]";
	mes "E ^FF0000Se falhar perderá a bota.^000000";
	mes "Ainda quer arriscar?";
	next;
	if (select( "Cancelar", "Sim ainda quero!" ) == 1) {
		mes "[Artesão de Hugin]";
		mes "Ok, volte quando quiser.";
		close;
	}
	mes "[Artesão de Hugin]";
	mes "Novamente, ^FF0000O Mestre Mágico não encantará nenhuma bota temporal com slot.^000000";
	mes "Deseja prosseguir?";
	next;
	if (select( "Cancelar", "Entendo. tente slotar." ) == 1) {
		mes "[Artesão de Hugin]";
		mes "Ok, volte quando quiser.";
		close;
	}
	if (countitem(6607) < 5) {
		mes "[Artesão de Hugin]";
		mes "Você deve trazer ^0000FF5 Cristais Temporais^000000. Nada é de graça...";
		close;
	}
	delitem 6607,5;// Temporal_Crystal
	delequip EQI_SHOES;
	if (rand(1,100) < 50) {
		mes "[Artesão de Hugin]";
		mes "Arrggg, o item quebrou. Mais sorte na próxima vez.";
		specialeffect2 EF_PHARMACY_FAIL;
		close;
	}
	mes "[Artesão de Hugin]";
	mes "Sucesso! Aqui está sua bota.";
	specialeffect2 EF_MAXPOWER;
	getitem getarg(0),1;
	close;
}