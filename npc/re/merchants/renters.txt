//===== Venus Script =========================================
//= Renewal Refining NPCs
//===== Por: =================================================
//= rAthena Dev Team / brAthena Dev Team / skb_ (CreativeSD Revision Translation)
//===== Versão atual: ========================================
//= 2.2
//===== Compatível com: ======================================
//= rAthena Project / Venus-Emulator
//===== Descrição: ===========================================
//= Renewal-specific breeder NPCs.
//===== Comentários Adicionais: ==============================
//= 2.1 Moved some renters to a separate renewal file. [Daegaladh]
//= 2.2 Updated to match the official scripts. [Euphy]
//============================================================

// Dragon Breeder
//============================================================
job3_rune01,88,62,5	script	Criador de Dragões	8W_SOLDIER,{
	mes "[Criador de Dragões]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune) {
		mes "Bem vindo. Você gostaria de alugar um Dragão como montaria?";
		next;
		switch(select("Sim:Não")) {
		case 1:
			//if (!getskilllv(RK_DRAGONTRAINING)) {
			if (!getskilllv("KN_RIDING")) {
				mes "[Criador de Dragões]";
				mes "Por favor, aprenda a habilidade Adestrar Dragão primeiro.";
				close;
			} else if (checkdragon()) {
				mes "[Criador de Dragões]";
				mes "Você já possui uma montaria.";
				close;
			} else if(ismounting()) {
				mes "[Criador de Dragões]";
				mes "Você já possui uma montaria.";
				close;
			}
			setdragon;
			close;
		case 2:
			mes "[Criador de Dragões]";
			mes "Entendo. Então tenha um ótimo dia.";
			close;
		}
	}
	mes "O que você está fazendo aqui?";
	mes "Somente Cavaleiros Rúnicos podem alugar Dragões.";
	close;
}

// Mestre de Dragões/Grifos
//============================================================
prontera,130,213,5	script	Mestre de Criaturas	8W_SOLDIER,{
	mes "[Mestre de Criaturas]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune) {
		mes "Bem vindo. Você gostaria de alugar um Dragão como montaria?";
		next;
		switch(select("Sim:Não")) {
		case 1:
			//if (!getskilllv(RK_DRAGONTRAINING)) {
			if (!getskilllv("KN_RIDING")) {
				mes "[Mestre de Criaturas]";
				mes "Por favor, aprenda a habilidade Adestrar Dragão primeiro.";
				close;
			} else if (checkdragon()) {
				mes "[Mestre de Criaturas]";
				mes "Você já possui uma montaria.";
				close;
			} else if(ismounting()) {
				mes "[Mestre de Criaturas]";
				mes "Você já possui uma montaria.";
				close;
			}
			setdragon;
			close;
		case 2:
			mes "[Mestre de Criaturas]";
			mes "Entendo. Então tenha um ótimo dia.";
			close;
		}
	}
	if (Class == Job_Royal_Guard || Class == Job_Royal_Guard_T || Class == Job_Baby_Guard) {
		mes "Bem vindo. Gostaria de alugar um Grifo como montaria?";
		next;
		switch(select("Sim:Não")) {
		case 1:
			if(!getskilllv("KN_RIDING")) {
				mes "[Mestre de Criaturas]";
				mes "Por favor, aprenda a habilidade de Montaria primeiro.";
				close;
			} else if (checkriding()) {
				mes "[Mestre de Criaturas]";
				mes "Você já possui uma montaria.";
				close;
			} else if(ismounting()) {
				mes "[Mestre de Criaturas]";
				mes "Você já possui uma montaria.";
				close;
			}
			setriding;
			close;
		case 2:
			mes "[Mestre de Criaturas]";
			mes "Entendo. Então tenha um ótimo dia.";
			close;
		}
	}
	mes "Eu estou aqui para fornecer a Cavaleiros Rúnicos e Guardiões Reais criaturas para montar.";
	close;
}

geffen,100,55,3	duplicate(Mestre de Criaturas)	Mestre de Criaturas#2	8W_SOLDIER
payon,166,102,5	duplicate(Mestre de Criaturas)	Mestre de Criaturas#3	8W_SOLDIER
aldebaran,133,109,5	duplicate(Mestre de Criaturas)	Mestre de Criaturas#4	8W_SOLDIER
yuno,171,187,3	duplicate(Mestre de Criaturas)	Mestre de Criaturas#5	8W_SOLDIER
rachel,106,130,5	duplicate(Mestre de Criaturas)	Mestre de Criaturas#6	8W_SOLDIER

// Removedor de Montaria
//============================================================
prontera,125,208,5	script	Removedor de Montaria	8W_SOLDIER,{
	mes "[Soldado]";
	mes "Se você não conseguir desmontar de um Peco Peco";
	mes "por alguma razão desconhecida,";
	mes "por favor, sinta-se livre para usar meus serviços.";
	if (checkriding()) {
		next;
		mes "[Soldado]";
		mes "Você está montado em um Peco Peco.";
		mes "Gostaria de desmontá-lo?";
		next;
		switch(select("Sim:Não")) {
		case 1:
			setriding 0;
			mes "[Soldado]";
			mes "Diga, como você se sente";
			mes "ao pisar no chão com os";
			mes "próprios pés novamente?";
			close;
		case 2:
			mes "[Soldado]";
			mes "Entendo. Sinta-se à vontade para conversar comigo novamente";
			mes "se você mudar de ideia.";
			close;
		}
	}
	close;
}

// Magic Gear Renter
//============================================================
-	script	::mgm	-1,{
	mes "[Mestre da Engrenagem]";
	if (Class == Job_Mechanic || Class == Job_Mechanic_T || Class == Job_Baby_Mechanic) {
		mes "Bem vindo, mecânico.";
		mes "Você gostaria de alugar um Carrinho ou";
		mes "montar em um M.E.C.H.A?";
		next;
		switch(select("Alugar um Carrinho:Alugar um M.E.C.H.A:Comprar um " + getitemname(23277) + ":Atualizar Dispositivo de Resfriamento:Cancelar")) {
		case 1:
			if (checkcart()) {
				mes "[Mestre da Engrenagem]";
				mes "Sinto muito, mas você já";
				mes "possui um carrinho.";
				close;
			}
			setcart;
			mes "[Mestre da Engrenagem]";
			mes "Ai está!";
			close;
		case 2:
			if (checkmadogear()) {
				mes "[Mestre da Engrenagem]";
				mes "Sinto muito, mas você já está";
				mes "montado em um M.E.C.H.A.";
				close;
			} else if (!getskilllv("NC_MADOLICENCE")) {
				mes "[Mestre da Engrenagem]";
				mes "Por favor, aprenda a habilidade Licença de Pilotagem primeiro.";
				close;
			} else if(ismounting()) {
				mes "[Mestre da Engrenagem]";
				mes "Por favor, remova a sua montaria atual primeiro.";
				close;
			}
			setmadogear;
			mes "[Mestre da Engrenagem]";
			mes "Divirta-se, e por favor, volte sempre que precisar!";
			close;
		case 3:
			mes "[Mestre da Engrenagem]";
			mes "O " + getitemname(23277) + " é realmente útil para situações de emergência e é vendido a 1.000.000 Zeny.";
			next;
			if (select("Comprar:Cancelar") == 2) {
				mes "[Mestre da Engrenagem]";
				mes "Entendo. Por favor, sinta-se livre para me consultar";
				mes "caso você mude de ideia.";
				close;
			}
			if (countitem(23277) > 0) {
				mes "[Mestre da Engrenagem]";
				mes "Me desculpe, mas você já tem uma " + getitemname(23277) + ".";
				close;
			}
			if (Zeny < 1000000) {
				mes "[Mestre da Engrenagem]";
				mes "Me desculpe, mas você não tem Zeny suficiente para comprar o " + getitemname(23277) + ".";
				close;
			}
			Zeny -= 1000000;
			getitem 23277,1; //Mado_Box
			mes "[Mestre da Engrenagem]";
			mes "Ai está!";
			close;
		case 4:
			mes "[Mestre da Engrenagem]";
			mes "Qual dispositivo você deseja atualizar?";
			next;
			if (select("Termoregulador:Resfriador Avançado") == 1) {
				mes "[Mestre da Engrenagem]";
				mes "A atualização de um Termoregulador para um Resfriador Avançado é necessário de 1 Termoregulador e 2.000.000 Zenys.";
				next;
				.@itemid = 2804;
				.@cost = 2000000;
			} else {
				mes "[Mestre da Engrenagem]";
				mes "A atualização de um Resfriador Avançado para um Resfriado Especial é necessário de 1 Resfriador Avançado e 4.000.000 Zenys.";
				next;
				.@itemid = 2809;
				.@cost = 4000000;
			}
			if (select("Atualizar:Cancelar") == 2) {
				mes "[Mestre da Engrenagem]";
				mes "Entendo. Por favor, fique a vontade para me consultar";
				mes "caso mude de ideia.";
				close;
			}
			if (!countitem(.@itemid)) {
				mes "[Mestre da Engrenagem]";
				mes "Me desculpe, mas você não tem o " + getitemname(.@itemid) + ".";
				close;
			}
			if (Zeny < .@cost) {
				mes "[Mestre da Engrenagem]";
				mes "Desculpe, mas você não tem Zeny suficiente para atualizar o dispositivo.";
				close;
			}
			Zeny -= .@cost;
			delitem .@itemid,1;
			getitem (.@itemid == 2804 ? 2809 : 2810),1; //High_Quality_Cooler,Special_Cooler
			mes "[Mestre da Engrenagem]";
			mes "Olha aqui! O seu próprio " + getitemname(.@itemid) + ".";
			close;
		case 5:
			close;
		}
	}
	mes "Será que eu posso ajuda-lo?";
	mes "M.E.C.H.A.S estão disponíveis apenas para Mecânicos.";
	close;
}
//prontera,163,178,3	duplicate(mgm)	Mestre da Engrenagem#prt	8W_SOLDIER
geffen,103,55,5	duplicate(mgm)	Mestre da Engrenagem#gef	8W_SOLDIER
payon,166,106,5	duplicate(mgm)	Mestre da Engrenagem#pay	8W_SOLDIER
aldebaran,133,112,5	duplicate(mgm)	Mestre da Engrenagem#alde	8W_SOLDIER
yuno,167,187,3	duplicate(mgm)	Mestre da Engrenagem#yuno	8W_SOLDIER
rachel,106,134,5	duplicate(mgm)	Mestre da Engrenagem#ra	8W_SOLDIER
dicastes01,187,207,3	duplicate(mgm)	Mestre da Engrenagem#dic	8W_SOLDIER
manuk,273,212,5	duplicate(mgm)	Mestre da Engrenagem#man	8W_SOLDIER
splendide,180,174,5	duplicate(mgm)	Mestre da Engrenagem#spl	8W_SOLDIER
mid_camp,242,243,3	duplicate(mgm)	Mestre da Engrenagem#mid	8W_SOLDIER