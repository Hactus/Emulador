//===== rAthena Script =======================================
//= Horror Toy Factory merchants
//===== Description: =========================================
//= [Official Conversion]
//= Horror Toy Factory enchants / exchange in xmas
//===== Changelogs: ==========================================
//= 1.0 First version. Uses official script structure and text
//=     from iRO. [Capuche]
//============================================================

xmas,240,291,3	script	Billy The Golden Hands#1	4_F_05,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "Você tem uma grande variedade de itens. Tente novamente quando tiver menos itens.";
		close;
	}
	if ((MaxWeight - Weight) < 10000) {
		mes "Você está sobrecarregado, não pode proceder. Tente novamente após reduzir o peso.";
		close;
	}
	mes "[Billy The Golden Hands]";
	mes "Huhu. Você trouxe algumas moedas para mim? Eu venderia tudo por moedas, menos minha alma.";
	next;

	setarray .@xm_item[0],
		22534, 100,		// Closedmind_Box
		18848, 500,		// Lush_Rose
		19687, 1000,	// C_Lush_Rose
		19686, 500,		// C_SantaHairband
		19701, 500,		// C_Red_Bonnet
		13442, 1000,	// Old_Parasol
		11563, 7,		// Hot_Tee
		11564, 5,		// Sweet_Canape
		523,   1,		// Holy_Water
		12020, 1;		// Water_Of_Darkness

	.@s = ( select( "Cancelar", "Closed Mind Box (100 Coins)", "Lush Rose (500 Coins)", "C Lush Rose (1000 Coins)", "C Santa Hairband (500 Coins)", "C Red Bonnet (500 Coins)", "Old Parasol (1000 Coins)", "Hot Tea (7 Coins)", "Sweet Canape (5 Coins)", "Holy Water (1 Coin)", "Water Of Darkness (1 Coin)" ) -2 ) * 2;
	if (.@s == -2) {
		mes "[Billy The Golden Hands]";
		mes "Khaha, você não tem escrúpulos? Tudo bem então. Volte quando tiver mudado de ideia.";
		close;
	}
	if (countitem(7642) >= .@xm_item[.@s+1]) {
		mes "[Billy The Golden Hands]";
		mes "Você precisa de ^0000ff" + .@xm_item[.@s+1] + "^000000 Moedas sangrentas para comprar ^0000ff" + getitemname(.@xm_item[.@s]) + "^000000. Você vai comprar?";
		next;
		if (select( "Cancelar", "Comprar" ) == 1) {
			mes "[Billy The Golden Hands]";
			mes "Khaha, que colega indeciso. Se decidir mesmo comprar, volte depois.";
			close;
		}
		if (countitem(7642) >= .@xm_item[.@s+1]) {
			delitem 7642,.@xm_item[.@s+1];// Bloody_Coin
			getitem .@xm_item[.@s], 1;
			mes "[Billy The Golden Hands]";
			mes "Você pagou por isso. Espero vê-lo novamente.!";
			close;
		}
	}
	mes "[Billy The Golden Hands]";
	mes "Você precisa de ^0000ff" + .@xm_item[.@s+1] + "^000000 Moedas sangrentas para comprar ^0000ff" + getitemname(.@xm_item[.@s]) + "^000000. Parece que faltam algumas.";
	close;
}


xmas,240,297,3	script	Vagrant Cain#1	4_M_06,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "Você tem uma grande variedade de itens. Tente novamente quando tiver menos itens.";
		close;
	}
	if ((MaxWeight - Weight) < 10000) {
		mes "Você está sobrecarregado, não pode proceder. Tente novamente após reduzir o peso.";
		close;
	}
	mes "[Vagrant Cain]";
	mes "Oh, eu tenho certeza que você veio aqui porque não gosta de coisas normais. Fale comigo. Eu farei tudo com preço justo e os materiais adequados.";
	next;
	.@s = select( "Nada que eu queira.", "Eu quero Celine's Ribbon.", "Eu quero Noble Cross.", "Eu quero Evilspirit Gloves." );
	switch(.@s) {
	case 1:
		mes "[Vagrant Cain]";
		mes "Sim, sim. Todo mundo diz isso na primeira vez. Vejo você depois.";
		close;
	case 2:
		.@string$[0] = "Ho~ Você busca um chapéu muito especial, meu amigo. Você possui grande discernimento.";
		.@string$[1] = "Eu preciso de ^0000ff1000 ^000000Moedas sangrentas e uma ^0000ff+7 ou superior Lush Rose^000000 para ^0000ffCeline's Ribbon^000000. Você tem todos os materiais?";
		.@string$[2] = "Você deve preparar ^0000ff1000 ^000000Moedas sangrentas e uma ^0000ff +7 ou superior Lush Rose^000000 para ^0000ffCeline's Ribbon^000000. Não se esqueça!";
		.@string$[3] = "Você tem certeza da troca? ^ff0000Os níveis de refinamento, os efeitos e as cartas vão desaparecer e eu não faço reembolso^000000. Pense nisso!";
		break;
	case 3:
		.@string$[0] = "Ho~ Você busca uma arma muito preciosa, meu amigo. Você possui grande discernimento.";
		.@string$[1] = "Eu preciso de ^0000ff2000 ^000000Moedas sangrentas e uma ^0000ffGrand Cross (slot)^000000 para ^0000ffNoble Cross^000000. Você tem todos os materiais?";
		.@string$[2] = "Você deve preparar ^0000ff 2000 ^000000Moedas sangrentas e uma ^0000ff Grand Cross (slot)^000000 para ^0000ffNoble Cross^000000. Não se esqueça!";
		.@string$[3] = "Você tem certeza da troca? ^ff0000Os níveis de refinamento, os efeitos e as cartas vão desaparecer e eu não faço reembolso^000000. Pense nisso!";
		break;
	case 4:
		.@string$[0] = "Ho~ Você busca um acessório raro, meu amigo.";
		.@string$[1] = "Eu preciso de ^0000ff1000 ^000000Moedas sangrentas, um ^0000ffHurt Mind, Kind Heart, e Red Lantern ^000000de cada para ^0000ffEvilspirit Gloves^000000. Você tem todos os materiais?";
		.@string$[2] = "Você deve preparar ^0000ff1000 ^000000Moedas sangrentas, um ^0000ffHurt Mind, Kind Heart, e Red Lantern ^000000de cada para ^0000ffEvilspirit Gloves^000000. Não se esqueça!";
		.@string$[3] = "Você tem certeza da troca? ^ff0000Os níveis de refinamento, os efeitos e as cartas vão desaparecer e eu não faço reembolso^000000. Além disso, se você tem o mesmo tipo de coisas, eu não sei qual escolher.Pense nisso.";
		break;
	}
	mes "[Vagrant Cain]";
	mes .@string$[0];
	next;
	mes "[Vagrant Cain]";
	mes .@string$[1];
	next;
	if (select( "Eu não tenho os itens agora", "Eu quero trocar agora" ) == 1) {
		mes "[Vagrant Cain]";
		mes "Volte quando estiver pronto. Deixe-me lembrá-lo dos materiais.";
		next;
		mes "[Vagrant Cain]";
		mes .@string$[2];
		close;
	}
	mes "[Vagrant Cain]";
	mes .@string$[3];
	next;
	if (select( "Vou tentar da próxima vez", "Troque os itens agora" ) == 1) {
		mes "[Vagrant Cain]";
		mes "Okay, você é determinado. Espero te ver de novo na próxima vez, huhu.";
		close;
	}
	switch(.@s) {
	case 2:
		mes "[Vagrant Cain]";
		if (getequipid(EQI_HEAD_TOP) != 18848)// Lush_Rose
			mes "O chapéu que você está usando não é um Lush Rose. Traga os materiais que eu pedi, kukuku.";
		else if (getequiprefinerycnt(EQI_HEAD_TOP) < 7)
			mes "O chapéu que você está usando não está refinado em +7 ou superior. Você não pode fazer a troca assim.";
		else if (countitem(7642) < 1000)
			mes "Bom, você tem poucas Moedas Sangrentas. Você precisa coletar exatamente 1000 delas. Menos que isso... Eu não posso ajudar.";
		else {
			delitem 7642,1000;// Bloody_Coin
			delequip EQI_HEAD_TOP;
			getitem 18849,1;// Celines_Ribbon
			mes "O negócio foi concluído. Veja como ficou.";// custom
		}
		close;
	case 3:
		mes "[Vagrant Cain]";
		if (getequipid(EQI_HAND_R) != 1540)// Grand_Cross_
			mes "O item na sua mão direita não é uma Grand Cross (slot). Traga o item certo que eu pedi, kuku.";
		else if (countitem(7642) < 2000)
			mes "Bom, você tem poucas Moedas Sangrentas. Você precisa coletar exatamente 2000 delas. Menos que isso... Eu não posso ajudar.";
		else {
			delitem 7642,2000;// Bloody_Coin
			delequip EQI_HAND_R;
			getitem 16029,1;// Noble_Cross
			mes "O negócio foi concluído. Veja como ficou.";// custom
		}
		close;
	case 4:
		mes "[Vagrant Cain]";
		if (countitem(7642) > 999 && countitem(2977) > 0 && countitem(2978) > 0 && countitem(2976) > 0) {
			delitem 7642,1000;// Bloody_Coin
			delitem 2977,1;// Hurt_Mind
			delitem 2978,1;// KindHeart
			delitem 2976,1;// Red_Lantern
			getitem 2980,1;// Evilspirit_Gloves
			mes "O negócio foi concluído. Veja como ficou.";// custom
		}
		else {
			mes "Você não tem materiais suficientes.";
			next;
			mes "[Vagrant Cain]";
			mes "Você deve preparar ^0000ff1000 ^000000Moedas sangrentas, um ^0000ffHurt Mind, Kind Heart e uma Red Lantern ^000000de cada para ^0000ffEvilspirit Gloves^000000.";
		}
		close;
	}
}

xmas,240,294,3	script	Black Beard Joe#pa0829	4_M_05,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "Você tem uma grande variedade de itens. Tente novamente quando tiver menos itens.";
		close;
	}
	if ((MaxWeight - Weight) < 10000) {
		mes "Você está sobrecarregado, não pode proceder. Tente novamente após reduzir o peso.";
		close;
	}
	mes "[Black Beard Joe]";
	mes "Huhu, a ganância do ser humano é naturalmente indesejada. O que você quer que eu faça?";
	next;
	switch( select( "O que você pode fazer por mim?", "Encantar acessório da Fábrica de Brinquedos", "Encantar arma da Fábrica de Brinquedos", "Encantar chapéu da Fábrica de Brinquedos", "Encantar calçados da Fábrica de Brinquedos" ) ) {
	case 1:
		mes "[Black Beard Joe]";
		mes "O que eu faço? É bem simples. Só me traga Moedas Sangrentas, vou pegar elas e encantar seu acessório.";
		next;
		mes "[Black Beard Joe]";
		mes "Eu vou pegar 15 Moedas Sangrentas em cada tentativa. Eu vou encantar um slot a cada 15 Moedas. Claro, 3 é o limite.";
		next;
		mes "[Black Beard Joe]";
		mes "Bom, isso é tão natural que a taxa de destruição ficará maior conforme o nível de aprimoramento aumenta. Eu recomendo você parar na primeira ou segunda tentativa, tee-hee.";
		close;
	case 2:
		.@eq_num = EQI_ACC_R;
		break;
	case 3:
		.@eq_num = EQI_HAND_R;
		break;
	case 4:
		.@eq_num = EQI_HEAD_TOP;
		break;
	case 5:
		.@eq_num = EQI_SHOES;
		break;
	}
	.@item_id = getequipid(.@eq_num);
	.@refine = getequiprefinerycnt(.@eq_num);
	setarray .@card[0],
		getequipcardid(.@eq_num,0),
		getequipcardid(.@eq_num,1),
		getequipcardid(.@eq_num,2),
		getequipcardid(.@eq_num,3);

	switch(.@item_id) {
	case 2976:	// Red_Lantern
	case 2977:	// Hurt_Mind
	case 2978:	// KindHeart
	case 2980:	// Evilspirit_Gloves
		.@sc_type = 1;// acc
		break;
	case 2486:	// Shadow_Walk_
	case 18848:	// Lush_Rose
	case 18849:	// Celines_Ribbon
		.@sc_type = 2;// armor
		break;
	case 13442:	// Old_Parasol
		.@sc_type = 3;// weapon
		break;
	case 16029:	// Noble_Cross
		mes "[Black Beard Joe]";// custom
		mes "Oh, você equipou a ^0000ffNoble Cross^000000! Você precisa encontrar outra pessoa para encantar esse item.";
		next;
		mes "[Black Beard Joe]";
		mes "Eu tentei diversas vezes, mas falhei.";
		next;
		mes "[Black Beard Joe]";
		mes "Se você quer encantar essa arma, fale com ^0000ffMayomayo^000000 na Ilha Malangdo! Ele deve conseguir.";
		close;
	default:
		break;
	}
	switch(.@eq_num) {
	case EQI_ACC_R:
		mes "[Black Beard Joe]";
		mes "Você pode escolher um dentre 2 tipos e a taxa para cada tentativa é 15 Moedas Sangrentas... Eu vou ficar com elas antes de fazer o encantamento.";
		next;
		.@enchant_type = select( "Sair", "Um atributo aleatório entre STR/AGI/DEX", "Um atributo aleatório entre INT/VIT/DEX" ) - 1;// enchant_type [1-2]
		mes "[Black Beard Joe]";
		if (.@enchant_type == 0) {
			mes "Okay, me procute novamente quando quiser fazer isso, kuku.";
			close;
		}
		if (.@sc_type < 1) {
			mes "Esse não é um equipamento da Fábrica de Brinquedos. Eu só trabalho com itens da Fábrica de Brinquedos.";
			close;
		}
		if (.@card[3] == 0) {
			.@slot_num = 3;
			mes "Okay, vamos tentar o primeiro encantamento. Normalmente, eles não quebram. Cerca de 95% de taxa de sucesso?";
			.@string$ = "Você é quase um libriano, amigo indeciso.!";
		}
		else if (.@card[2] == 0) {
			.@slot_num = 2;
			mes "Ho~ Segundo encantamento. Agora fica perigoso. A taxa de sucesso é cerca de 70%. Você quer continuar? O acessório será destuído se eu falhar.";// custom
			.@string$ = "Você é quase um libriano, amigo indeciso.!";
		}
		else if (.@card[1] == 0) {
			.@slot_num = 1;
			mes "Esse é o último encantamento. Você quer continuar? Agora, ^ff0000a taxa de sucesso é inferior a 50%^000000. Claro, seria excelente se eu conseguisse... Hmm, eu vou acompanhar sua decisão, mas lembre-se disso.";// custom
			.@string$ = "Sábia decisão! É muita ganância fazer o terceiro encantamento.";// custom translation
		}
		else {
			mes "Ho~ Esse item já atingiu o limite. É impossível fazer um encantamento maior, então seja feliz e use com sabedoria.";// custom
			close;
		}
		break;
	case EQI_HAND_R:
		mes "[Black Beard Joe]";
		if (.@sc_type < 1) {
			mes "Esse não é um equipamento da Fábrica de Brinquedos. Eu só trabalho com itens da Fábrica de Brinquedos.";
			close;
		}
		.@enchant_type = 3;
		.@slot_num = 3;
		mes "Eu encanto armas somente uma vez. Não falha, então você pode pensar nisso como um efeito bônus, tee-hee.";
		.@string$ = "Você é quase um libriano, amigo indeciso.!";
		break;
	case EQI_HEAD_TOP:
	case EQI_SHOES:
		mes "[Black Beard Joe]";
		if (.@sc_type < 1) {
			mes "Esse não é um equipamento da Fábrica de Brinquedos. Eu só trabalho com itens da Fábrica de Brinquedos.";
			close;
		}
		if (.@card[3] == 0) {
			.@slot_num = 3;
			.@enchant_type = 4;
			mes "Bom! Vamos começar o primeiro encantamento. Eu vou adicionar um dos efeitos especiais de batalha. Claro, você não precisa ter medo, não tem chance de falhar, kuku.";
		}
		else if (.@card[2] == 0) {
			.@slot_num = 2;
			.@enchant_type = 5;
			mes "O segundo encantamento. Eu vou melhorar as condições anteriores. Também não há chance de falhar, não se preocupe, kukuku";// custom
		}
		else {
			mes "Ho~ Esse item já atingiu o limite. É impossível fazer um encantamento maior, então seja feliz e use com sabedoria.";// custom
			close;
		}
		.@string$ = "Você é quase um libriano, amigo indeciso.!";
		break;
	default:
		mes "[Black Beard Joe]";
		mes "Eu não encanto isso!";// custom
		close;
	}
	next;
	if (select( "Eu vou voltar depois", "Faça, por favor" ) == 1) {
		mes "[Black Beard Joe]";
		mes .@string$;
		close;
	}
	mes "[Black Beard Joe]";
	if (.@slot_num == 3)
		setarray .@range[0],550,1050;
	else if (.@slot_num == 2)
		setarray .@range[0],450,1125;
	else if (.@slot_num == 1)
		setarray .@range[0],1,1161;
	else {
		mes "Ocorreu um problema, volte novamente depois!";// custom
		close;
	}
	.@enchant = 9;
	switch(.@enchant_type) {
	case 1:
		.@r = rand(.@range[0],.@range[1]);
			 if (.@r < 601)  .@enchant = 0;
		else if (.@r < 701)  .@enchant = 4700;	// Strength1
		else if (.@r < 801)  .@enchant = 4730;	// Agility1
		else if (.@r < 901)  .@enchant = 4720;	// Dexterity1
		else if (.@r < 951)  .@enchant = 4701;	// Strength2
		else if (.@r < 1001) .@enchant = 4731;	// Agility2
		else if (.@r < 1051) .@enchant = 4721;	// Dexterity2
		else if (.@r < 1076) .@enchant = 4702;	// Strength3
		else if (.@r < 1101) .@enchant = 4732;	// Agility3
		else if (.@r < 1126) .@enchant = 4722;	// Dexterity3
		else if (.@r < 1138) .@enchant = 4703;	// Strength4
		else if (.@r < 1150) .@enchant = 4733;	// Agility4
		else if (.@r < 1162) .@enchant = 4723;	// Dexterity4
		else .@enchant = 9;
		break;
	case 2:
		.@r = rand(.@range[0],.@range[1]);
			 if (.@r < 601)  .@enchant = 0;
		else if (.@r < 701)  .@enchant = 4710;	// Inteligence1
		else if (.@r < 801)  .@enchant = 4740;	// Vitality1
		else if (.@r < 901)  .@enchant = 4720;	// Dexterity1
		else if (.@r < 951)  .@enchant = 4711;	// Inteligence2
		else if (.@r < 1001) .@enchant = 4741;	// Vitality2
		else if (.@r < 1051) .@enchant = 4721;	// Dexterity2
		else if (.@r < 1076) .@enchant = 4712;	// Inteligence3
		else if (.@r < 1101) .@enchant = 4742;	// Vitality3
		else if (.@r < 1126) .@enchant = 4722;	// Dexterity3
		else if (.@r < 1138) .@enchant = 4713;	// Inteligence4
		else if (.@r < 1150) .@enchant = 4743;	// Vitality4
		else if (.@r < 1162) .@enchant = 4723;	// Dexterity4
		else .@enchant = 9;
		break;
	case 3:
		.@r = rand(1,2555);
			 if (.@r < 301)  .@enchant = 4820;	// Fighting_Spirit5
		else if (.@r < 501)  .@enchant = 4821;	// Fighting_Spirit6
		else if (.@r < 601)  .@enchant = 4822;	// Fighting_Spirit7
		else if (.@r < 651)  .@enchant = 4823;	// Fighting_Spirit8
		else if (.@r < 676)  .@enchant = 4824;	// Fighting_Spirit9
		else if (.@r < 686)  .@enchant = 4825;	// Fighting_Spirit10
		else if (.@r < 986)  .@enchant = 4816;	// Sharp3
		else if (.@r < 1086) .@enchant = 4843;	// Sharp4
		else if (.@r < 1096) .@enchant = 4844;	// Sharp5
		else if (.@r < 1396) .@enchant = 4760;	// Matk1
		else if (.@r < 1496) .@enchant = 4761;	// Matk2
		else if (.@r < 1506) .@enchant = 4806;	// Matk3
		else if (.@r < 1806) .@enchant = 4872;	// Attack_Delay_2
		else if (.@r < 2006) .@enchant = 4873;	// Attack_Delay_3
		else if (.@r < 2106) .@enchant = 4881;	// Attack_Delay_4
		else if (.@r < 2116) .@enchant = 4807;	// Atk_Speed1
		else if (.@r < 2416) .@enchant = 4827;	// Spell6
		else if (.@r < 2516) .@enchant = 4828;	// Spell7
		else if (.@r < 2546) .@enchant = 4829;	// Spell8
		else if (.@r < 2556) .@enchant = 4830;	// Spell9
		else .@enchant = 9;
		break;
	case 4:
		.@r = rand(1,1950);
			 if (.@r < 301)  .@enchant = 4810;	// Fighting_Spirit2
		else if (.@r < 401)  .@enchant = 4809;	// Fighting_Spirit3
		else if (.@r < 451)  .@enchant = 4808;	// Fighting_Spirit4
		else if (.@r < 471)  .@enchant = 4820;	// Fighting_Spirit5
		else if (.@r < 771)  .@enchant = 4818;	// Sharp1
		else if (.@r < 871)  .@enchant = 4817;	// Sharp2
		else if (.@r < 891)  .@enchant = 4816;	// Sharp3
		else if (.@r < 991)  .@enchant = 4760;	// Matk1
		else if (.@r < 1011) .@enchant = 4761;	// Matk2
		else if (.@r < 1311) .@enchant = 4869;	// Attack_Delay_1
		else if (.@r < 1411) .@enchant = 4872;	// Attack_Delay_2
		else if (.@r < 1461) .@enchant = 4873;	// Attack_Delay_3
		else if (.@r < 1481) .@enchant = 4881;	// Attack_Delay_4
		else if (.@r < 1781) .@enchant = 4813;	// Spell3
		else if (.@r < 1881) .@enchant = 4812;	// Spell4
		else if (.@r < 1931) .@enchant = 4826;	// Spell5
		else if (.@r < 1951) .@enchant = 4827;	// Spell6
		else .@enchant = 9;
		break;
	case 5:
		.@r = rand(1,1970);
			 if (.@r < 301)  .@enchant = 4700;	// Strength1
		else if (.@r < 401)  .@enchant = 4701;	// Strength2
		else if (.@r < 451)  .@enchant = 4702;	// Strength3
		else if (.@r < 471)  .@enchant = 4730;	// Agility1
		else if (.@r < 771)  .@enchant = 4731;	// Agility2
		else if (.@r < 871)  .@enchant = 4732;	// Agility3
		else if (.@r < 891)  .@enchant = 4710;	// Inteligence1
		else if (.@r < 991)  .@enchant = 4711;	// Inteligence2
		else if (.@r < 1011) .@enchant = 4712;	// Inteligence3
		else if (.@r < 1311) .@enchant = 4720;	// Dexterity1
		else if (.@r < 1411) .@enchant = 4721;	// Dexterity2
		else if (.@r < 1461) .@enchant = 4722;	// Dexterity3
		else if (.@r < 1481) .@enchant = 4740;	// Vitality1
		else if (.@r < 1781) .@enchant = 4741;	// Vitality2
		else if (.@r < 1881) .@enchant = 4742;	// Vitality3
		else if (.@r < 1931) .@enchant = 4750;	// Luck1
		else if (.@r < 1951) .@enchant = 4751;	// Luck2
		else if (.@r < 1971) .@enchant = 4752;	// Luck3
		else .@enchant = 9;
		break;
	default:
		break;
	}
	if (.@enchant == 9) {
		mes "Ocorreu um problema, volte novamente depois!";// custom
		close;
	}
	if (.@card[3] == 0 && .@sc_type < 4) 
		.@card[3] = .@enchant;
	else if (.@card[2] == 0 && .@sc_type < 3)
		.@card[2] = .@enchant;
	else if (.@card[1] == 0 && .@sc_type < 2)
		.@card[1] = .@enchant;
	else {
		mes "Eu não sei que item é esse, mas acho que não é algo com o qual eu possa trabalhar.";// custom
		close;
	}
	if (countitem(7642) > 14) {// Bloody_Coin
		delitem 7642,15;// Bloody_Coin
		delequip .@eq_num;
		if (.@enchant == 0) {
			mes "Ouch! Ai de você, o encantamento falhou e seu item quebrou, credo!";
			close2;
			specialeffect2 EF_LORD;
			end;
		}
		getitem2 .@item_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
		mes "Hmm.. It was successful. Take a look at it.";
		specialeffect2 EF_REPAIRWEAPON;
		close;
	}
	mes "Hmm.. Por sinal, você precisa de mais Moedas Sangrentas. Você não pode pagar a taxa.";
	close;
}